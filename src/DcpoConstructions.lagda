Tom de Jong, 27 May 2019.

* Dcpo of continuous functions (i.e. the exponential in the category of dcpos)
* Continuous K and S functions
* The lifting of a set is a dcpo
* Continuous ifZero function specific to the lifting of the natural numbers

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-PropTrunc hiding (⊥)

module DcpoConstructions
        (pt : propositional-truncations-exist)
        (fe : ∀ {𝓤 𝓥} → funext 𝓤 𝓥)
       where

open PropositionalTruncation pt
open import UF-Miscelanea
open import UF-Subsingletons hiding (⊥)
open import UF-Subsingletons-FunExt

open import NaturalsAddition renaming (_+_ to _+'_)
open import NaturalsOrder
open import NaturalNumbers-Properties

\end{code}

We start by defining the dcpo of continuous functions. This is the exponential
(or internal hom) in the category of dcpos (hence, the notation ⟹ᵈᶜᵖᵒ).

\begin{code}

module DcpoConstructionsGeneral
        (𝓥 : Universe)
       where
 open import Dcpo pt fe 𝓥

 module _ (𝓓 : DCPO {𝓤} {𝓣})
          (𝓔 : DCPO {𝓤'} {𝓣'})
        where

  _hom-⊑_ : DCPO[ 𝓓 , 𝓔 ] → DCPO[ 𝓓 , 𝓔 ] → 𝓤 ⊔ 𝓣' ̇
  (f , _) hom-⊑ (g , _) = ∀ d → f d ⊑⟨ 𝓔 ⟩ g d

  pointwise-family : {I : 𝓥 ̇} (α : I → DCPO[ 𝓓 , 𝓔 ]) → ⟨ 𝓓 ⟩ → I → ⟨ 𝓔 ⟩
  pointwise-family α d i = underlying-function 𝓓 𝓔 (α i) d

  pointwise-family-is-directed : {I : 𝓥 ̇} (α : I → DCPO[ 𝓓 , 𝓔 ])
                                 (δ : is-directed _hom-⊑_ α)
                                 (d : ⟨ 𝓓 ⟩)
                               → is-directed (underlying-order 𝓔)
                                  (pointwise-family α d)
  pointwise-family-is-directed {I} α δ d =
   (is-directed-gives-inhabited _hom-⊑_ α δ) ,
   λ (i j : I) → ∥∥-functor (h i j) ((is-directed-order _hom-⊑_ α δ) i j)
    where
     β : ⟨ 𝓓 ⟩ → I → ⟨ 𝓔 ⟩
     β = pointwise-family α
     h : (i j : I) → (Σ k ꞉ I , α i hom-⊑ α k × α j hom-⊑ α k)
         → Σ (\k → (β d i) ⊑⟨ 𝓔 ⟩ (β d k) × (β d j) ⊑⟨ 𝓔 ⟩ (β d k))
     h i j (k , l , m) = k , l d , m d

  continuous-functions-sup : {I : 𝓥 ̇} (α : I → DCPO[ 𝓓 , 𝓔 ])
                           → is-directed _hom-⊑_ α → DCPO[ 𝓓 , 𝓔 ]
  continuous-functions-sup {I} α δ = f , c
   where
    β : ⟨ 𝓓 ⟩ → I → ⟨ 𝓔 ⟩
    β d = pointwise-family α d
    ε : (d : ⟨ 𝓓 ⟩) → is-directed (underlying-order 𝓔) (β d)
    ε = pointwise-family-is-directed α δ
    f : ⟨ 𝓓 ⟩ → ⟨ 𝓔 ⟩
    f d = ∐ 𝓔 {I} {β d} (ε d)
    c : is-continuous 𝓓 𝓔 f
    c J γ φ = u , v
     where
      u : (j : J) → f (γ j) ⊑⟨ 𝓔 ⟩ f (∐ 𝓓 φ)
      u j = ∐-is-lowerbound-of-upperbounds 𝓔 (ε (γ j)) (f (∐ 𝓓 φ)) r
       where
        r : (i : I) → underlying-function 𝓓 𝓔 (α i) (γ j) ⊑⟨ 𝓔 ⟩ f (∐ 𝓓 φ)
        r i = transitivity 𝓔
              (underlying-function 𝓓 𝓔 (α i) (γ j))
              (underlying-function 𝓓 𝓔 (α i) (∐ 𝓓 φ))
              (f (∐ 𝓓 φ)) p q
         where
          p : underlying-function 𝓓 𝓔 (α i) (γ j) ⊑⟨ 𝓔 ⟩
              underlying-function 𝓓 𝓔 (α i) (∐ 𝓓 φ)
          p = continuous-functions-are-monotone 𝓓 𝓔 (α i) (γ j) (∐ 𝓓 φ)
              (∐-is-upperbound 𝓓 φ j)
          q : underlying-function 𝓓 𝓔 (α i) (∐ 𝓓 φ) ⊑⟨ 𝓔 ⟩ f (∐ 𝓓 φ)
          q = ∐-is-upperbound 𝓔 (ε (∐ 𝓓 φ)) i
      v : (y : ⟨ 𝓔 ⟩)
        → ((j : J) → f (γ j) ⊑⟨ 𝓔 ⟩ y)
        → f (∐ 𝓓 φ) ⊑⟨ 𝓔 ⟩ y
      v y l = ∐-is-lowerbound-of-upperbounds 𝓔 (ε (∐ 𝓓 φ)) y r
       where
        r : (i : I) → β (∐ 𝓓 φ) i ⊑⟨ 𝓔 ⟩ y
        r i = transitivity 𝓔 (β (∐ 𝓓 φ) i) (f (∐ 𝓓 φ)) y p q
         where
          p : β (∐ 𝓓 φ) i ⊑⟨ 𝓔 ⟩ f (∐ 𝓓 φ)
          p = ∐-is-upperbound 𝓔 (ε (∐ 𝓓 φ)) i
          q : f (∐ 𝓓 φ) ⊑⟨ 𝓔 ⟩ y
          q = ∐-is-lowerbound-of-upperbounds 𝓔 (ε (∐ 𝓓 φ)) y h
           where
            h : (i' : I) → β (∐ 𝓓 φ) i' ⊑⟨ 𝓔 ⟩ y
            h i' = is-sup-gives-is-lowerbound-of-upperbounds (underlying-order 𝓔)
                   (continuity-of-function 𝓓 𝓔 (α i') J γ φ) y m
             where
              m : (j : J) → underlying-function 𝓓 𝓔 (α i') (γ j) ⊑⟨ 𝓔 ⟩ y
              m j = transitivity 𝓔
                    (underlying-function 𝓓 𝓔 (α i') (γ j)) (f (γ j)) y m₁ m₂
               where
                m₁ : underlying-function 𝓓 𝓔 (α i') (γ j) ⊑⟨ 𝓔 ⟩ (f (γ j))
                m₁ = ∐-is-upperbound 𝓔 (ε (γ j)) i'
                m₂ : f (γ j) ⊑⟨ 𝓔 ⟩ y
                m₂ = l j

 infixr 20 _⟹ᵈᶜᵖᵒ_

 _⟹ᵈᶜᵖᵒ_ : DCPO {𝓤} {𝓣} → DCPO {𝓤'} {𝓣'}
         → DCPO {(𝓥 ⁺) ⊔ 𝓤 ⊔ 𝓣 ⊔ 𝓤' ⊔ 𝓣'} {𝓤 ⊔ 𝓣'}
 𝓓 ⟹ᵈᶜᵖᵒ 𝓔 = DCPO[ 𝓓 , 𝓔 ] , _⊑_ , d
  where
   _⊑_ = 𝓓 hom-⊑ 𝓔
   d : dcpo-axioms _⊑_
   d = s , p , r , t , a , c
    where
     s : is-set DCPO[ 𝓓 , 𝓔 ]
     s = subsets-of-sets-are-sets (⟨ 𝓓 ⟩ → ⟨ 𝓔 ⟩) (is-continuous 𝓓 𝓔)
         (Π-is-set fe (λ (x : ⟨ 𝓓 ⟩) →  sethood 𝓔))
         (λ {f} → being-continuous-is-a-prop 𝓓 𝓔 f)
     p : (f g : DCPO[ 𝓓 , 𝓔 ]) → is-prop (f ⊑ g)
     p (f , _) (g , _) = Π-is-prop fe
                         (λ (x : ⟨ 𝓓 ⟩) → prop-valuedness 𝓔 (f x) (g x))
     r : (f : DCPO[ 𝓓 , 𝓔 ]) → f ⊑ f
     r (f , _) x = reflexivity 𝓔 (f x)
     t : (f g h : DCPO[ 𝓓 , 𝓔 ]) → f ⊑ g → g ⊑ h → f ⊑ h
     t (f , _) (g , _) (h , _) l m x = transitivity 𝓔 (f x) (g x) (h x)
                                       (l x) (m x)
     a : (f g : DCPO[ 𝓓 , 𝓔 ]) → f ⊑ g → g ⊑ f → f ≡ g
     a f g l m =
      to-Σ-≡
       (dfunext fe
        (λ d → antisymmetry 𝓔
               ((underlying-function 𝓓 𝓔 f) d)
               ((underlying-function 𝓓 𝓔 g) d)
               (l d) (m d)) ,
       being-continuous-is-a-prop 𝓓 𝓔 (underlying-function 𝓓 𝓔 g) _
        (continuity-of-function 𝓓 𝓔 g))
     c : (I : _ ̇ ) (α : I → DCPO[ 𝓓 , 𝓔 ]) → is-directed _⊑_ α → has-sup _⊑_ α
     c I α δ = (continuous-functions-sup 𝓓 𝓔 α δ) , u , v
      where
       u : (i : I) → α i ⊑ continuous-functions-sup 𝓓 𝓔 α δ
       u i d = ∐-is-upperbound 𝓔 (pointwise-family-is-directed 𝓓 𝓔 α δ d) i
       v : (g : DCPO[ 𝓓 , 𝓔 ])
         → ((i : I) → α i ⊑ g)
         → continuous-functions-sup 𝓓 𝓔 α δ ⊑ g
       v (g , _) l d = ∐-is-lowerbound-of-upperbounds 𝓔
                       (pointwise-family-is-directed 𝓓 𝓔 α δ d)
                       (g d) (λ (i : I) → l i d)

 infixr 20 _⟹ᵈᶜᵖᵒ⊥_

 _⟹ᵈᶜᵖᵒ⊥_ : DCPO⊥ {𝓤} {𝓣} → DCPO⊥ {𝓤'} {𝓣'}
          → DCPO⊥ {(𝓥 ⁺) ⊔ 𝓤 ⊔ 𝓣 ⊔ 𝓤' ⊔ 𝓣'} {𝓤 ⊔ 𝓣'}
 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓔 = ⟪ 𝓓 ⟫ ⟹ᵈᶜᵖᵒ ⟪ 𝓔 ⟫ , h
  where
   h : has-least (underlying-order (⟪ 𝓓 ⟫ ⟹ᵈᶜᵖᵒ ⟪ 𝓔 ⟫))
   h = ((λ _ → the-least 𝓔) ,
       constant-functions-are-continuous ⟪ 𝓓 ⟫ ⟪ 𝓔 ⟫ (the-least 𝓔)) ,
       (λ g d → least-property 𝓔 (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓔 ⟫ g d))

\end{code}

We proceed by defining continuous K and S functions.
This will be used in ScottModelOfPCF.

\begin{code}

 module _ (𝓓 : DCPO {𝓤} {𝓣})
          (𝓔 : DCPO {𝓤'} {𝓣'})
        where

  Kᵈᶜᵖᵒ : DCPO[ 𝓓 , 𝓔 ⟹ᵈᶜᵖᵒ 𝓓 ]
  Kᵈᶜᵖᵒ = k , c where
   k : ⟨ 𝓓 ⟩ → DCPO[ 𝓔 , 𝓓 ]
   k x = (λ _ → x) , (constant-functions-are-continuous 𝓔 𝓓 x)
   c : (I : 𝓥 ̇ ) (α : I → ⟨ 𝓓 ⟩) (δ : is-Directed 𝓓 α)
     → is-sup (underlying-order (𝓔 ⟹ᵈᶜᵖᵒ 𝓓)) (k (∐ 𝓓 δ)) (λ (i : I) → k (α i))
   c I α δ = u , v where
    u : (i : I) (e : ⟨ 𝓔 ⟩) → α i ⊑⟨ 𝓓 ⟩ (∐ 𝓓 δ)
    u i e = ∐-is-upperbound 𝓓 δ i
    v : (f : DCPO[ 𝓔 , 𝓓 ])
      → ((i : I) → k (α i) ⊑⟨ 𝓔 ⟹ᵈᶜᵖᵒ 𝓓 ⟩ f)
      → (e : ⟨ 𝓔 ⟩) → ∐ 𝓓 δ ⊑⟨ 𝓓 ⟩ (underlying-function 𝓔 𝓓 f e)
    v (f , _) l e = ∐-is-lowerbound-of-upperbounds 𝓓 δ (f e)
                    (λ (i : I) → (l i) e)

  module _ (𝓕 : DCPO {𝓦} {𝓦'}) where

   S₀ᵈᶜᵖᵒ : DCPO[ 𝓓 , 𝓔 ⟹ᵈᶜᵖᵒ 𝓕 ]
          → DCPO[ 𝓓 , 𝓔 ]
          → DCPO[ 𝓓 , 𝓕 ]
   S₀ᵈᶜᵖᵒ (f , cf) (g , cg) = (λ x → underlying-function 𝓔 𝓕 (f x) (g x)) , c
    where
     c : is-continuous 𝓓 𝓕 (λ x → underlying-function 𝓔 𝓕 (f x) (g x))
     c I α δ = u , v
      where
       u : (i : I) → underlying-function 𝓔 𝓕 (f (α i)) (g (α i)) ⊑⟨ 𝓕 ⟩
                     underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) (g (∐ 𝓓 δ))
       u i = transitivity 𝓕
             (underlying-function 𝓔 𝓕 (f (α i)) (g (α i)))
             (underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) (g (α i)))
             (underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) (g (∐ 𝓓 δ)))
             (l (g (α i)))
             (continuous-functions-are-monotone 𝓔 𝓕 (f (∐ 𝓓 δ)) (g (α i))
              (g (∐ 𝓓 δ)) m)
        where
         l : f (α i) ⊑⟨ 𝓔 ⟹ᵈᶜᵖᵒ 𝓕 ⟩ f (∐ 𝓓 δ)
         l = continuous-functions-are-monotone 𝓓 (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) (f , cf) (α i)
             (∐ 𝓓 δ) (∐-is-upperbound 𝓓 δ i)
         m : g (α i) ⊑⟨ 𝓔 ⟩ g (∐ 𝓓 δ)
         m = continuous-functions-are-monotone 𝓓 𝓔 (g , cg) (α i) (∐ 𝓓 δ)
             (∐-is-upperbound 𝓓 δ i)
       v : (y : ⟨ 𝓕 ⟩)
         → ((i : I) → (underlying-function 𝓔 𝓕 (f (α i)) (g (α i))) ⊑⟨ 𝓕 ⟩ y)
         → (underlying-function 𝓔 𝓕  (f (∐ 𝓓 δ)) (g (∐ 𝓓 δ))) ⊑⟨ 𝓕 ⟩ y
       v y ineqs = γ
        where
         γ : underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) (g (∐ 𝓓 δ)) ⊑⟨ 𝓕 ⟩ y
         γ = transport (λ - → underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) - ⊑⟨ 𝓕 ⟩ y)
             e₀ γ₀
          where
           e₀ : ∐ 𝓔 (image-is-directed 𝓓 𝓔 (g , cg) δ) ≡ g (∐ 𝓓 δ)
           e₀ = (continuous-function-∐-≡ 𝓓 𝓔 (g , cg) δ) ⁻¹
           ε₀ : is-Directed 𝓔 (g ∘ α)
           ε₀ = image-is-directed 𝓓 𝓔 (g , cg) δ
           γ₀ : (underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) (∐ 𝓔 ε₀)) ⊑⟨ 𝓕 ⟩ y
           γ₀ = transport (λ - → - ⊑⟨ 𝓕 ⟩ y) e₁ γ₁
            where
             e₁ : ∐ 𝓕 (image-is-directed 𝓔 𝓕 (f (∐ 𝓓 δ)) ε₀) ≡
                  underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) (∐ 𝓔 ε₀)
             e₁ = (continuous-function-∐-≡ 𝓔 𝓕 (f (∐ 𝓓 δ)) ε₀) ⁻¹
             ε₁ : is-Directed 𝓕
                  (underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ)) ∘ (g ∘ α))
             ε₁ = image-is-directed 𝓔 𝓕 (f (∐ 𝓓 δ)) ε₀
             γ₁ : (∐ 𝓕 ε₁) ⊑⟨ 𝓕 ⟩ y
             γ₁ = ∐-is-lowerbound-of-upperbounds 𝓕 ε₁ y γ₂
              where
               γ₂ : (i : I)
                  → (underlying-function 𝓔 𝓕 (f (∐ 𝓓 δ))) (g (α i)) ⊑⟨ 𝓕 ⟩ y
               γ₂ i = transport
                      (λ - → (underlying-function 𝓔 𝓕 -) (g (α i)) ⊑⟨ 𝓕 ⟩ y )
                      e₂ γ₃
                where
                 ε₂ : is-Directed (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) (f ∘ α)
                 ε₂ = image-is-directed 𝓓 (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) (f , cf) δ
                 e₂ : ∐ (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) ε₂ ≡ f (∐ 𝓓 δ)
                 e₂ = (continuous-function-∐-≡ 𝓓 (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) (f , cf) δ) ⁻¹
                 γ₃ : underlying-function 𝓔 𝓕 (∐ (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) {I} {f ∘ α} ε₂)
                       (g (α i))
                      ⊑⟨ 𝓕 ⟩ y
                 γ₃ = ∐-is-lowerbound-of-upperbounds 𝓕
                       (pointwise-family-is-directed 𝓔 𝓕 (f ∘ α) ε₂ (g (α i)))
                       y h
                  where
                   h : (j : I) → (pr₁ (f (α j)) (g (α i))) ⊑⟨ 𝓕 ⟩ y
                   h j = ∥∥-rec (prop-valuedness 𝓕 (pr₁ (f (α j)) (g (α i))) y)
                         r (is-Directed-order 𝓓 α δ i j)
                    where
                     r : (Σ  k ꞉ I , α i ⊑⟨ 𝓓 ⟩ α k × α j ⊑⟨ 𝓓 ⟩ α k)
                       → (underlying-function 𝓔 𝓕 (f (α j)) (g (α i))) ⊑⟨ 𝓕 ⟩ y
                     r (k , l , m ) = transitivity 𝓕
                                       (underlying-function 𝓔 𝓕 (f (α j))
                                        (g (α i)))
                                       (underlying-function 𝓔 𝓕 (f (α k))
                                        (g (α k)))
                                       y
                                       (transitivity 𝓕
                                         (underlying-function 𝓔 𝓕 (f (α j))
                                           (g (α i)))
                                         (underlying-function 𝓔 𝓕 (f (α k))
                                           (g (α i)))
                                       (underlying-function 𝓔 𝓕 (f (α k))
                                           (g (α k)))
                                       (s (g (α i)))
                                       (continuous-functions-are-monotone 𝓔 𝓕
                                         (f (α k)) (g (α i)) (g (α k))
                                        (continuous-functions-are-monotone 𝓓 𝓔
                                         (g , cg) (α i) (α k) l)))
                                      (ineqs k)
                      where
                       s : f (α j) ⊑⟨ 𝓔 ⟹ᵈᶜᵖᵒ 𝓕 ⟩ f (α k)
                       s = continuous-functions-are-monotone 𝓓
                            (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) (f , cf) (α j) (α k) m


   S₁ᵈᶜᵖᵒ : DCPO[ 𝓓 , 𝓔 ⟹ᵈᶜᵖᵒ 𝓕 ]
          → DCPO[ 𝓓 ⟹ᵈᶜᵖᵒ 𝓔 , 𝓓 ⟹ᵈᶜᵖᵒ 𝓕 ]
   S₁ᵈᶜᵖᵒ (f , cf) = h , c
    where
     h : DCPO[ 𝓓 , 𝓔 ] → DCPO[ 𝓓 , 𝓕 ]
     h = (S₀ᵈᶜᵖᵒ (f , cf))
     c : is-continuous (𝓓 ⟹ᵈᶜᵖᵒ 𝓔) (𝓓 ⟹ᵈᶜᵖᵒ 𝓕) h
     c I α δ = u , v
      where
       u : (i : I) (d : ⟨ 𝓓 ⟩)
         → underlying-function 𝓓 𝓕 (h (α i)) d ⊑⟨ 𝓕 ⟩
           underlying-function 𝓓 𝓕 (h (∐ (𝓓 ⟹ᵈᶜᵖᵒ 𝓔) {I} {α} δ)) d
       u i d = continuous-functions-are-monotone 𝓔 𝓕 (f d)
               (underlying-function 𝓓 𝓔 (α i) d)
               (underlying-function 𝓓 𝓔 (∐ (𝓓 ⟹ᵈᶜᵖᵒ 𝓔) {I} {α} δ) d)
               (∐-is-upperbound 𝓔 (pointwise-family-is-directed 𝓓 𝓔 α δ d) i)
       v : (g : DCPO[ 𝓓 , 𝓕 ])
         → ((i : I) → h (α i) ⊑⟨ 𝓓 ⟹ᵈᶜᵖᵒ 𝓕 ⟩ g)
         → (d : ⟨ 𝓓 ⟩) → underlying-function 𝓓 𝓕 (h (∐ (𝓓 ⟹ᵈᶜᵖᵒ 𝓔)
                           {I} {α} δ)) d
                          ⊑⟨ 𝓕 ⟩ underlying-function 𝓓 𝓕 g d
       v g l d = transport (λ - → - ⊑⟨ 𝓕 ⟩ underlying-function 𝓓 𝓕 g d) e s
        where
         ε : is-Directed 𝓔 (pointwise-family 𝓓 𝓔 α d)
         ε = pointwise-family-is-directed 𝓓 𝓔 α δ d
         e : ∐ 𝓕 (image-is-directed 𝓔 𝓕 (f d) ε)
             ≡ underlying-function 𝓓 𝓕 (h (∐ (𝓓 ⟹ᵈᶜᵖᵒ 𝓔) {I} {α} δ)) d
         e = (continuous-function-∐-≡ 𝓔 𝓕 (f d) ε) ⁻¹
         φ : is-Directed 𝓕
             (underlying-function 𝓔 𝓕 (f d) ∘ (pointwise-family 𝓓 𝓔 α d))
         φ = image-is-directed 𝓔 𝓕 (f d) ε
         s : ∐ 𝓕 φ ⊑⟨ 𝓕 ⟩ (underlying-function 𝓓 𝓕 g) d
         s = ∐-is-lowerbound-of-upperbounds 𝓕 φ (underlying-function 𝓓 𝓕 g d)
             (λ (i : I) → l i d)

   Sᵈᶜᵖᵒ : DCPO[ 𝓓 ⟹ᵈᶜᵖᵒ 𝓔 ⟹ᵈᶜᵖᵒ 𝓕 , (𝓓 ⟹ᵈᶜᵖᵒ 𝓔) ⟹ᵈᶜᵖᵒ (𝓓 ⟹ᵈᶜᵖᵒ 𝓕) ]
   Sᵈᶜᵖᵒ = S₁ᵈᶜᵖᵒ , c
    where
     c : is-continuous (𝓓 ⟹ᵈᶜᵖᵒ 𝓔 ⟹ᵈᶜᵖᵒ 𝓕)
                       ((𝓓 ⟹ᵈᶜᵖᵒ 𝓔) ⟹ᵈᶜᵖᵒ (𝓓 ⟹ᵈᶜᵖᵒ 𝓕))
                       S₁ᵈᶜᵖᵒ
     c I α δ = u , v
      where
       u : (i : I) (g : DCPO[ 𝓓 , 𝓔 ]) (d : ⟨ 𝓓 ⟩)
         → pr₁ (pr₁ (α i) d) (pr₁ g d)
           ⊑⟨ 𝓕 ⟩ pr₁ (pr₁ (∐ (𝓓 ⟹ᵈᶜᵖᵒ (𝓔 ⟹ᵈᶜᵖᵒ 𝓕)) {I} {α} δ) d) (pr₁ g d)
       u i g d = ∐-is-upperbound 𝓕
                 (pointwise-family-is-directed 𝓔 𝓕 β ε (pr₁ g d)) i
        where
         β : I → DCPO[ 𝓔 , 𝓕 ]
         β = pointwise-family 𝓓 (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) α d
         ε : is-Directed (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) β
         ε = pointwise-family-is-directed 𝓓 (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) α δ d
       v : (f : DCPO[ 𝓓 ⟹ᵈᶜᵖᵒ 𝓔 , 𝓓 ⟹ᵈᶜᵖᵒ 𝓕 ])
         → ((i : I) → S₁ᵈᶜᵖᵒ (α i) ⊑⟨ (𝓓 ⟹ᵈᶜᵖᵒ 𝓔) ⟹ᵈᶜᵖᵒ (𝓓 ⟹ᵈᶜᵖᵒ 𝓕) ⟩ f)
         → (g : DCPO[ 𝓓 , 𝓔 ]) (d : ⟨ 𝓓 ⟩)
           → pr₁ (pr₁ (∐ (𝓓 ⟹ᵈᶜᵖᵒ (𝓔 ⟹ᵈᶜᵖᵒ 𝓕)) {I} {α} δ) d) (pr₁ g d)
             ⊑⟨ 𝓕 ⟩ (pr₁ (pr₁ f g) d)
       v f l g d = ∐-is-lowerbound-of-upperbounds 𝓕 ε (pr₁ (pr₁ f g) d)
                   (λ (i : I) → l i g d)
        where
         ε : is-Directed 𝓕 (λ (i : I) → pr₁ (pr₁ (S₁ᵈᶜᵖᵒ (α i)) g) d)
         ε = pointwise-family-is-directed 𝓔 𝓕 β φ (underlying-function 𝓓 𝓔 g d)
          where
           β : I → DCPO[ 𝓔 , 𝓕 ]
           β i = underlying-function 𝓓 (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) (α i) d
           φ : is-Directed (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) β
           φ = pointwise-family-is-directed 𝓓 (𝓔 ⟹ᵈᶜᵖᵒ 𝓕) α δ d

 module _ (𝓓 : DCPO⊥ {𝓤} {𝓣})
          (𝓔 : DCPO⊥ {𝓤'} {𝓣'})
        where

  Kᵈᶜᵖᵒ⊥ : DCPO⊥[ 𝓓 , 𝓔 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ]
  Kᵈᶜᵖᵒ⊥ = Kᵈᶜᵖᵒ ⟪ 𝓓 ⟫ ⟪ 𝓔 ⟫

  Sᵈᶜᵖᵒ⊥ : (𝓕 : DCPO⊥ {𝓦} {𝓦'})
         → DCPO⊥[ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓔 ⟹ᵈᶜᵖᵒ⊥ 𝓕 , (𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓔) ⟹ᵈᶜᵖᵒ⊥ (𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓕) ]
  Sᵈᶜᵖᵒ⊥ 𝓕 = Sᵈᶜᵖᵒ ⟪ 𝓓 ⟫ ⟪ 𝓔 ⟫ ⟪ 𝓕 ⟫

\end{code}

Next is the construction of the least fixed point operator for dcpos with bottom.
At the end, we have to specialise to 𝓤₀-dcpos (directed completeness for the
lowest universe), because ℕ lives in 𝓤₀.

\begin{code}

 module _ (𝓓 : DCPO⊥ {𝓤} {𝓣}) where

  iter : (n : ℕ) → ⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩ → ⟨ ⟪ 𝓓 ⟫ ⟩
  iter zero     f = the-least 𝓓
  iter (succ n) f = underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ f (iter n f)

  iter-is-monotone : (n : ℕ) → is-monotone ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ (iter n)
  iter-is-monotone zero     f g l = least-property 𝓓 (iter zero g)
  iter-is-monotone (succ n) f g l =
   transitivity ⟪ 𝓓 ⟫
    (iter (succ n) f)
    (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ g (iter n f))
    (iter (succ n) g)
    (l (iter n f))
    (continuous-functions-are-monotone ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ g (iter n f) (iter n g)
     (iter-is-monotone n f g l))

  n-family : {I : 𝓥 ̇} (α : I → ⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩) (n : ℕ) → I → ⟨ ⟪ 𝓓 ⟫ ⟩
  n-family α n i = iter n (α i)

  n-family-is-directed : {I : 𝓥 ̇} (α : I → ⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩)
                         (δ : is-Directed ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ α)
                         (n : ℕ) → is-Directed ⟪ 𝓓 ⟫ (n-family α n)
  n-family-is-directed {I} α δ n =
    is-Directed-gives-inhabited ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ α δ , ε
   where
    ε : (i j : I) →  ∃  k ꞉ I , (n-family α n i) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ (n-family α n k) ×
                                  (n-family α n j) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ (n-family α n k)
    ε i j = ∥∥-functor h (is-Directed-order ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ α δ i j)
     where
      h : (Σ  k ꞉ I , (α i) ⊑⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩ (α k) ×
                        (α j) ⊑⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩ (α k))
          → Σ  k ꞉ I , (n-family α n i) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ (n-family α n k) ×
                         (n-family α n j) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ (n-family α n k)
      h (k , l , m) = k , (iter-is-monotone n (α i) (α k) l) ,
                      (iter-is-monotone n (α j) (α k) m)

  double-∐-lemma : {I : 𝓥 ̇} (α : I → ⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩)
                   (δ : is-Directed ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ α)
                   (n : ℕ)
                 → ∐ ⟪ 𝓓 ⟫ (pointwise-family-is-directed ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ α δ
                    (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)))
                   ≡ ∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ (succ n))
  double-∐-lemma {I} α δ n = antisymmetry ⟪ 𝓓 ⟫ x y a b
   where
    ε : is-Directed ⟪ 𝓓 ⟫ (pointwise-family ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ α
         (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)))
    ε = (pointwise-family-is-directed ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ α δ
         (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)))
    φ : (n : ℕ) → is-Directed ⟪ 𝓓 ⟫ (n-family α n)
    φ n = n-family-is-directed α δ n

    x : ⟨ ⟪ 𝓓 ⟫ ⟩
    x = ∐ ⟪ 𝓓 ⟫ ε
    y : ⟨ ⟪ 𝓓 ⟫ ⟩
    y = ∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ (succ n))

    a : x ⊑⟨ ⟪ 𝓓 ⟫ ⟩ y
    a = ∐-is-lowerbound-of-upperbounds ⟪ 𝓓 ⟫ ε y g
     where
      g : (i : I)
        → (pointwise-family ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ α (∐ ⟪ 𝓓 ⟫ (φ n)) i) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ y
      g i = is-sup-gives-is-lowerbound-of-upperbounds
             (underlying-order ⟪ 𝓓 ⟫) s y u
       where
        β : I → ⟨ ⟪ 𝓓 ⟫ ⟩
        β = underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α i) ∘ (n-family α n)
        s : is-sup (underlying-order ⟪ 𝓓 ⟫)
            (pointwise-family ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ α (∐ ⟪ 𝓓 ⟫ (φ n)) i) β
        s = continuity-of-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α i) I (n-family α n) (φ n)
        u : (j : I) → underlying-order ⟪ 𝓓 ⟫ (β j) y
        u j = ∥∥-rec (prop-valuedness ⟪ 𝓓 ⟫ (β j) y) v
               (is-Directed-order ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ α δ i j)
                where
          v : (Σ  k ꞉ I , α i ⊑⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩ α k
                          × α j ⊑⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟩ α k)
            → β j ⊑⟨ ⟪ 𝓓 ⟫ ⟩ y
          v (k , l , m) = transitivity ⟪ 𝓓 ⟫ (β j) (iter (succ n) (α k)) y p q
           where
            q : iter (succ n) (α k) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ y
            q = ∐-is-upperbound ⟪ 𝓓 ⟫ (φ (succ n)) k
            p : β j ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter (succ n) (α k)
            p = transitivity ⟪ 𝓓 ⟫
                (β j)
                (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α k) (iter n (α j)))
                (iter (succ n) (α k))
                p₀ p₁
             where
              p₀ : β j ⊑⟨ ⟪ 𝓓 ⟫ ⟩ underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α k)
                                   (iter n (α j))
              p₀ = l (iter n (α j))
              p₁ : underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α k) (iter n (α j))
                   ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter (succ n) (α k)
              p₁ = continuous-functions-are-monotone ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α k)
                   (iter n (α j))
                   (iter n (α k))
                   (iter-is-monotone n (α j) (α k) m)

    b : y ⊑⟨ ⟪ 𝓓 ⟫ ⟩ x
    b = ∐-is-lowerbound-of-upperbounds ⟪ 𝓓 ⟫ (φ (succ n)) x h
     where
      h : (i : I) → (n-family α (succ n) i) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ x
      h i = transitivity ⟪ 𝓓 ⟫ (n-family α (succ n) i)
             (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α i) (∐ ⟪ 𝓓 ⟫ (φ n))) x p q
       where
        p : iter (succ n) (α i) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫
             (α i) (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n))
        p = continuous-functions-are-monotone ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α i)
             (iter n (α i))
             (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n))
             (∐-is-upperbound ⟪ 𝓓 ⟫ (φ n) i)
        q : (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ (α i)
             (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)))
            ⊑⟨ ⟪ 𝓓 ⟫ ⟩ x
        q = ∐-is-upperbound ⟪ 𝓓 ⟫ ε i

  iter-is-continuous : (n : ℕ) → is-continuous ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ (iter n)
  iter-is-continuous zero     I α δ = a , b
   where
    a : (i : I) → iter zero (α i) ⊑⟨ ⟪ 𝓓 ⟫ ⟩
                  iter zero (∐ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ {I} {α} δ)
    a i = least-property 𝓓 (iter zero (∐ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ {I} {α} δ))
    b : (u : ⟨ ⟪ 𝓓 ⟫ ⟩)
      → ((i : I) → iter zero (α i) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ u)
      → iter zero (∐ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ {I} {α} δ) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ u
    b u l = least-property 𝓓 u
  iter-is-continuous (succ n) I α δ = γ
   where
    γ : is-sup (underlying-order ⟪ 𝓓 ⟫)
        (iter (succ n) (∐ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ δ)) (iter (succ n) ∘ α)
    γ = transport
        (λ - → is-sup (underlying-order ⟪ 𝓓 ⟫) - (iter (succ n) ∘ α)) (h ⁻¹) k
     where
      k : is-sup (underlying-order ⟪ 𝓓 ⟫)
          (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ (succ n)))
          (iter (succ n) ∘ α)
      k = ∐-is-sup ⟪ 𝓓 ⟫ (n-family-is-directed α δ (succ n))
      h = iter (succ n) s

               ≡⟨ refl ⟩

          underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ s (iter n s)

               ≡⟨ ap (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ s) e ⟩

          underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ s (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n))

               ≡⟨ refl ⟩

          ∐ ⟪ 𝓓 ⟫ (pointwise-family-is-directed ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ α δ
           (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)))

               ≡⟨ double-∐-lemma α δ n ⟩

          ∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ (succ n))
               ∎
       where
        s = (∐ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ {I} {α} δ)
        e : iter n s ≡ ∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)
        e = antisymmetry ⟪ 𝓓 ⟫ (iter n s) (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)) l m
         where
          IH : is-sup (underlying-order ⟪ 𝓓 ⟫) (iter n (∐ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ δ))
               (iter n ∘ α)
          IH = iter-is-continuous n I α δ
          l : iter n s ⊑⟨ ⟪ 𝓓 ⟫ ⟩ ∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)
          l = is-sup-gives-is-lowerbound-of-upperbounds
              (underlying-order ⟪ 𝓓 ⟫) IH
              (∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n))
              (∐-is-upperbound ⟪ 𝓓 ⟫ (n-family-is-directed α δ n))
          m : ∐ ⟪ 𝓓 ⟫ (n-family-is-directed α δ n) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter n s
          m = ∐-is-lowerbound-of-upperbounds ⟪ 𝓓 ⟫ (n-family-is-directed α δ n)
              (iter n s)
              (is-sup-gives-is-upperbound (underlying-order ⟪ 𝓓 ⟫) IH)

  iter-c : ℕ → DCPO[ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ]
  iter-c n = iter n , iter-is-continuous n

  iter-is-ω-chain : (n : ℕ) → (iter-c n) ⊑⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟹ᵈᶜᵖᵒ ⟪ 𝓓 ⟫ ⟩
                              (iter-c (succ n))
  iter-is-ω-chain zero     f = least-property 𝓓 (iter (succ zero) f)
  iter-is-ω-chain (succ n) f = continuous-functions-are-monotone ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ f
                               (iter n f)
                               (iter (succ n) f)
                               (iter-is-ω-chain n f)

  iter-increases : (n m : ℕ) → (n ≤ m)
                 → (iter-c n) ⊑⟨ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟹ᵈᶜᵖᵒ ⟪ 𝓓 ⟫ ⟩ (iter-c m)
  iter-increases n zero l     f = transport
                                  (λ - → iter - f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter zero f)
                                  (unique-minimal n l ⁻¹)
                                  (reflexivity ⟪ 𝓓 ⟫ (iter zero f))
  iter-increases n (succ m) l f = h (≤-split n m l)
   where
    h : (n ≤ m) + (n ≡ succ m) → (iter n f) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter (succ m) f
    h (inl l') = transitivity ⟪ 𝓓 ⟫ (iter n f) (iter m f) (iter (succ m) f)
                 (iter-increases n m l' f)
                 (iter-is-ω-chain m f)
    h (inr e)  = transport (λ - → iter - f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter (succ m) f) (e ⁻¹)
                 (reflexivity ⟪ 𝓓 ⟫ (iter (succ m) f))

module _ where

 open import Dcpo pt fe 𝓤₀
 open DcpoConstructionsGeneral 𝓤₀

 module _ (𝓓 : DCPO⊥ {𝓤} {𝓣}) where

  iter-is-directed : is-directed (λ F G → ∀ f → F f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ G f) (iter 𝓓)
  iter-is-directed = ∣ zero ∣ , δ
   where
    δ : (i j : ℕ)
      → ∃ k ꞉ ℕ , ((f : DCPO[ ⟪ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ]) → iter 𝓓 i f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter 𝓓 k f)
                × ((f : DCPO[ ⟪ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ]) → iter 𝓓 j f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter 𝓓 k f)
    δ i j = ∣ i +' j , l , m ∣
     where
      l : (f : DCPO[ ⟪ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ]) → iter 𝓓 i f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter 𝓓 (i +' j) f
      l = iter-increases 𝓓 i (i +' j)
          (cosubtraction i (i +' j) (j , (addition-commutativity j i)))
      m : (f : DCPO[ ⟪ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ]) → iter 𝓓 j f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ iter 𝓓 (i +' j) f
      m = iter-increases 𝓓 j (i +' j) (cosubtraction j (i +' j) (i , refl))

  μ : DCPO[ ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ]
  μ = continuous-functions-sup ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ (iter-c 𝓓) iter-is-directed

  μ-gives-a-fixed-point : (f : DCPO[ ⟪ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ])
                        → underlying-function ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ μ f
                          ≡ (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ f
                            (underlying-function ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ μ f))
  μ-gives-a-fixed-point fc = antisymmetry ⟪ 𝓓 ⟫ (ν fc) (f (ν fc)) l m
   where
    ν : DCPO[ ⟪ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ] → ⟨ ⟪ 𝓓 ⟫ ⟩
    ν = underlying-function ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ μ
    f : ⟨ ⟪ 𝓓 ⟫ ⟩ → ⟨ ⟪ 𝓓 ⟫ ⟩
    f = underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ fc
    δ : is-directed (underlying-order ⟪ 𝓓 ⟫)
     (pointwise-family ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ (iter-c 𝓓) fc)
    δ = pointwise-family-is-directed ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ (iter-c 𝓓)
        iter-is-directed fc

    l : ν fc ⊑⟨ ⟪ 𝓓 ⟫ ⟩ f (ν fc)
    l = ∐-is-lowerbound-of-upperbounds ⟪ 𝓓 ⟫ δ (f (ν fc)) h
     where
      h : (n : ℕ) → iter 𝓓 n fc ⊑⟨ ⟪ 𝓓 ⟫ ⟩ f (ν fc)
      h zero     = least-property 𝓓 (f (ν fc))
      h (succ n) = continuous-functions-are-monotone ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ fc
                   (iter 𝓓 n fc)
                   (ν fc)
                   (∐-is-upperbound ⟪ 𝓓 ⟫ δ n)

    m : f (ν fc) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ ν fc
    m = is-sup-gives-is-lowerbound-of-upperbounds (underlying-order ⟪ 𝓓 ⟫)
        (continuity-of-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ fc ℕ α δ) (ν fc) k
     where
      α : ℕ → ⟨ ⟪ 𝓓 ⟫ ⟩
      α = pointwise-family ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ (iter-c 𝓓) fc
      k : (n : ℕ) → underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ fc (α n) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ ν fc
      k n = transitivity ⟪ 𝓓 ⟫
            (f (α n)) (α (succ n)) (ν fc)
            p q
       where
        p : underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ fc (α n) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ α (succ n)
        p = reflexivity ⟪ 𝓓 ⟫ (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ fc (α n))
        q : α (succ n) ⊑⟨ ⟪ 𝓓 ⟫ ⟩ ν fc
        q = ∐-is-upperbound ⟪ 𝓓 ⟫ δ (succ n)

  μ-gives-lowerbound-of-fixed-points :
      (f : DCPO[ ⟪ 𝓓 ⟫ , ⟪ 𝓓 ⟫ ])
      (d : ⟨ ⟪ 𝓓 ⟫ ⟩)
    → underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ f d  ⊑⟨ ⟪ 𝓓 ⟫ ⟩ d
    → (underlying-function ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ μ) f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ d
  μ-gives-lowerbound-of-fixed-points f d l =
   ∐-is-lowerbound-of-upperbounds ⟪ 𝓓 ⟫
   (pointwise-family-is-directed ⟪ 𝓓 ⟹ᵈᶜᵖᵒ⊥ 𝓓 ⟫ ⟪ 𝓓 ⟫ (iter-c 𝓓)
    iter-is-directed f)
   d g
    where
     g : (n : ℕ) → iter 𝓓 n f ⊑⟨ ⟪ 𝓓 ⟫ ⟩ d
     g zero     = least-property 𝓓 d
     g (succ n) = transitivity ⟪ 𝓓 ⟫
                  (iter 𝓓 (succ n) f) (underlying-function ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ f d) d
                  (continuous-functions-are-monotone ⟪ 𝓓 ⟫ ⟪ 𝓓 ⟫ f
                    (iter 𝓓 n f) d (g n))
                  l

\end{code}

In the following we show that the lifting of a set is a 𝓤₀-dcpo with bottom.

\begin{code}

 module LiftingDcpo
         (𝓣 : Universe)
         (pe : propext 𝓣)
        where

  open import UF-ImageAndSurjection
  open ImageAndSurjection pt

  open import Lifting 𝓣
  open import LiftingMiscelanea 𝓣
  open import LiftingMiscelanea-PropExt-FunExt 𝓣 pe fe
  open import LiftingMonad 𝓣

  module _ {𝓤 : Universe}
           {X : 𝓤 ̇ }
           (s : is-set X)
         where

   family-value-map : {I : 𝓤₀ ̇}
                    → (α : I → 𝓛 X)
                    → (Σ i ꞉ I , is-defined (α i)) → X
   family-value-map α (i , d) = value (α i) d

   directed-family-value-map-is-wconstant : {I : 𝓤₀ ̇}
                                          → (α : I → 𝓛 X)
                                          → (δ : is-directed _⊑'_ α )
                                          → wconstant (family-value-map α)
   directed-family-value-map-is-wconstant {I} α δ (i₀ , d₀) (i₁ , d₁) =
    γ (is-directed-order _⊑'_ α δ i₀ i₁)
     where
      f : Σ (λ i → is-defined (α i)) → X
      f = family-value-map α
      γ : (∃ k ꞉ I , (α i₀ ⊑' α k) × (α i₁ ⊑' α k)) → f (i₀ , d₀) ≡ f (i₁ , d₁)
      γ = ∥∥-rec s g
       where
        g : (Σ k ꞉ I , (α i₀ ⊑' α k)
                     × (α i₁ ⊑' α k)) → f (i₀ , d₀) ≡ f (i₁ , d₁)
        g (k , l , m) =
         f (i₀ , d₀)                             ≡⟨ refl ⟩
         value (α i₀) d₀                         ≡⟨ ≡-of-values-from-≡ (l d₀) ⟩
         value (α k) (≡-to-is-defined (l d₀) d₀) ≡⟨ ≡-of-values-from-≡ ((m d₁) ⁻¹) ⟩
         value (α i₁) d₁                         ≡⟨ refl ⟩
         f (i₁ , d₁)                             ∎

   lifting-sup-value : {I : 𝓤₀ ̇}
                     → (α : I → 𝓛 X)
                     → (δ : is-directed _⊑'_ α )
                     → (∃ i ꞉ I , is-defined (α i)) → X
   lifting-sup-value {I} α δ =
    wconstant-map-to-set-truncation-of-domain-map
     (Σ i ꞉ I , is-defined (α i))
     s (family-value-map α) (directed-family-value-map-is-wconstant α δ)

   lifting-sup : {I : 𝓤₀ ̇} → (α : I → 𝓛 X) → (δ : is-directed _⊑'_ α) → 𝓛 X
   lifting-sup {I} α δ =
    (∃ i ꞉ I , is-defined (α i)) , lifting-sup-value α δ , ∥∥-is-a-prop

   lifting-sup-is-upperbound : {I : 𝓤₀ ̇} → (α : I → 𝓛 X)
                               (δ : is-directed _⊑'_ α)
                             → (i : I) → α i ⊑' lifting-sup α δ
   lifting-sup-is-upperbound {I} α δ i = γ
    where
     γ : α i ⊑' lifting-sup α δ
     γ = ⊑-to-⊑' (f , v)
      where
       f : is-defined (α i) → is-defined (lifting-sup α δ)
       f d = ∣ i , d ∣
       v : (d : is-defined (α i)) → value (α i) d ≡ value (lifting-sup α δ) (f d)
       v d = value (α i) d                 ≡⟨ p ⟩
             lifting-sup-value α δ (f d)   ≡⟨ refl ⟩
             value (lifting-sup α δ) (f d) ∎
        where
         p = wconstant-map-to-set-factors-through-truncation-of-domain
              (Σ j ꞉ I , is-defined (α j)) s
              (family-value-map α)
              (directed-family-value-map-is-wconstant α δ)
              (i , d)

   family-defined-somewhere-sup-≡ : {I : 𝓤₀ ̇} {α : I → 𝓛 X}
                                  → (δ : is-directed _⊑'_ α)
                                  → (i : I)
                                  → is-defined (α i)
                                  → α i ≡ lifting-sup α δ
   family-defined-somewhere-sup-≡ {I} {α} δ i d =
    (lifting-sup-is-upperbound α δ i) d

   lifting-sup-is-lowerbound-of-upperbounds : {I : 𝓤₀ ̇}
                                            → {α : I → 𝓛 X}
                                            → (δ : is-directed _⊑'_ α)
                                            → (v : 𝓛 X)
                                            → ((i : I) → α i ⊑' v)
                                            → lifting-sup α δ ⊑' v
   lifting-sup-is-lowerbound-of-upperbounds {I} {α} δ v b = h
    where
     h : lifting-sup α δ ⊑' v
     h d = ∥∥-rec (lifting-of-set-is-a-set s) g d
      where
       g : (Σ i ꞉ I , is-defined (α i)) → lifting-sup α δ ≡ v
       g (i , dᵢ) = lifting-sup α δ ≡⟨ (family-defined-somewhere-sup-≡ δ i dᵢ) ⁻¹ ⟩
                    α i             ≡⟨ b i dᵢ ⟩
                    v               ∎

   𝓛-DCPO : DCPO {𝓣 ⁺ ⊔ 𝓤} {𝓣 ⁺ ⊔ 𝓤}
   𝓛-DCPO = 𝓛 X , _⊑'_ , sl , p , r , t , a , c
    where
     sl : is-set (𝓛 X)
     sl = lifting-of-set-is-a-set s
     p : is-prop-valued (_⊑'_)
     p _ _ = ⊑'-prop-valued s
     r : is-reflexive (_⊑'_)
     r _ = ⊑'-is-reflexive
     a : is-antisymmetric (_⊑'_)
     a _ _ = ⊑'-is-antisymmetric
     t : is-transitive (_⊑'_)
     t _ _ _ = ⊑'-is-transitive
     c : (I : 𝓤₀ ̇ ) (α : I → 𝓛 X) → is-directed _⊑'_ α → has-sup _⊑'_ α
     c I α δ = lifting-sup α δ ,
               lifting-sup-is-upperbound α δ ,
               lifting-sup-is-lowerbound-of-upperbounds δ

   𝓛-DCPO⊥ : DCPO⊥ {𝓣 ⁺ ⊔ 𝓤} {𝓣 ⁺ ⊔ 𝓤}
   𝓛-DCPO⊥ = 𝓛-DCPO , ⊥ , λ _ → unique-from-𝟘

\end{code}

Now that we have the lifting as a dcpo, we prove that the lifting functor and
Kleisli extension yield continuous maps

\begin{code}

  module _ {𝓤 : Universe}
           {X : 𝓤 ̇ }
           (s₀ : is-set X)
           {𝓥 : Universe}
           {Y : 𝓥 ̇ }
           (s₁ : is-set Y)
         where

   ♯-is-monotone : (f : X → 𝓛 Y) → is-monotone (𝓛-DCPO s₀) (𝓛-DCPO s₁) (f ♯)
   ♯-is-monotone f l m ineq d = ap (f ♯) (ineq (♯-is-defined f l d))

   ♯-is-continuous : (f : X → 𝓛 Y) → is-continuous (𝓛-DCPO s₀) (𝓛-DCPO s₁) (f ♯)
   ♯-is-continuous f I α δ = u , v
    where
     u : (i : I) → (f ♯) (α i) ⊑⟨ (𝓛-DCPO s₁) ⟩ (f ♯) (∐ (𝓛-DCPO s₀) δ)
     u i = ♯-is-monotone f (α i) (∐ (𝓛-DCPO s₀) δ)
           (∐-is-upperbound (𝓛-DCPO s₀) δ i)
     v : (m : ⟨ 𝓛-DCPO s₁ ⟩)
       → ((i : I) → (f ♯) (α i) ⊑⟨ (𝓛-DCPO s₁) ⟩ m)
       → (f ♯) (∐ (𝓛-DCPO s₀) δ) ⊑⟨ (𝓛-DCPO s₁) ⟩ m
     v m ineqs d =
      ∥∥-rec (lifting-of-set-is-a-set s₁) g (♯-is-defined f (∐ (𝓛-DCPO s₀) δ) d)
       where
        g : (Σ i ꞉ I , is-defined (α i)) → (f ♯) (∐ (𝓛-DCPO s₀) δ) ≡ m
        g (i , dᵢ) = (f ♯) (∐ (𝓛-DCPO s₀) δ) ≡⟨ h i dᵢ ⟩
                     (f ♯) (α i)             ≡⟨ ineqs i (≡-to-is-defined (h i dᵢ) d) ⟩
                     m                       ∎
         where
          h : (i : I) → is-defined (α i) → (f ♯) (∐ (𝓛-DCPO s₀) δ) ≡ (f ♯) (α i)
          h i d = ap (f ♯) ((family-defined-somewhere-sup-≡ s₀ δ i d) ⁻¹)

   𝓛̇-continuous : (f : X → Y) → is-continuous (𝓛-DCPO s₀) (𝓛-DCPO s₁) (𝓛̇ f)
   𝓛̇-continuous f = transport
                     (is-continuous (𝓛-DCPO s₀) (𝓛-DCPO s₁))
                     (dfunext fe (𝓛̇-♯-∼ f))
                     (♯-is-continuous (η ∘ f))

\end{code}

Finally, we construct the ifZero function, specific to the lifting of ℕ.
Again, this will be used in ScottModelOfPCF.

The continuity proofs are not very appealing and the second proof could perhaps
be simplified by exploiting the "symmetry" of ifZero: for example,
ifZero a b 0 ≡ ifZero b a 1).
The second proof is essentially identical to the
first proof; the only difference is that we have to introduce an additional
parameter in the second proof. We leave simplifications of the proofs for
future work.

\begin{code}

  𝓛ᵈℕ : DCPO⊥
  𝓛ᵈℕ = 𝓛-DCPO⊥ ℕ-is-set

  ⦅ifZero⦆₀ : 𝓛 ℕ → 𝓛 ℕ → ℕ → 𝓛 ℕ
  ⦅ifZero⦆₀ a b zero     = a
  ⦅ifZero⦆₀ a b (succ n) = b

  ⦅ifZero⦆₁ : 𝓛 ℕ → 𝓛 ℕ → DCPO⊥[ 𝓛ᵈℕ , 𝓛ᵈℕ ]
  ⦅ifZero⦆₁ a b =
   (⦅ifZero⦆₀ a b) ♯ , ♯-is-continuous ℕ-is-set ℕ-is-set (⦅ifZero⦆₀ a b)

  ⦅ifZero⦆₂ : 𝓛 ℕ → DCPO⊥[ 𝓛ᵈℕ , 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ]
  ⦅ifZero⦆₂ a = ⦅ifZero⦆₁ a , c
   where
    c : is-continuous ⟪ 𝓛ᵈℕ ⟫ ⟪ 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ⟫ (⦅ifZero⦆₁ a)
    c I α δ = u , v
     where
      u : (i : I) (l : 𝓛 ℕ) (d : is-defined (((⦅ifZero⦆₀ a (α i)) ♯) l))
        → ((⦅ifZero⦆₀ a (α i)) ♯) l ≡ ((⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) ♯) l
      u i l d = ((⦅ifZero⦆₀ a (α i)) ♯) l              ≡⟨ q₀ ⟩
                ⦅ifZero⦆₀ a (α i) (value l e)          ≡⟨ q₁ ⟩
                ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e) ≡⟨ q₂ ⟩
                ((⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) ♯) l     ∎
       where
        e : is-defined l
        e = ♯-is-defined (⦅ifZero⦆₀ a (α i)) l d
        p₀ : value l e ≡ zero → ⦅ifZero⦆₀ a (α i) (value l e)
           ≡ ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
        p₀ q = ⦅ifZero⦆₀ a (α i) (value l e)
                  ≡⟨ ap (⦅ifZero⦆₀ a (α i)) q ⟩
               ⦅ifZero⦆₀ a (α i) zero
                  ≡⟨ refl ⟩
               a
                  ≡⟨ refl ⟩
               ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) zero
                  ≡⟨ ap (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) (q ⁻¹) ⟩
               ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                  ∎
        pₛ : (n : ℕ) → value l e ≡ succ n → ⦅ifZero⦆₀ a (α i) (value l e)
                                          ≡ ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
        pₛ n q = ⦅ifZero⦆₀ a (α i) (value l e)
                    ≡⟨ ap (⦅ifZero⦆₀ a (α i)) q ⟩
                 ⦅ifZero⦆₀ a (α i) (succ n)
                    ≡⟨ refl ⟩
                 α i
                    ≡⟨ family-defined-somewhere-sup-≡ ℕ-is-set δ i e₁ ⟩
                 ∐ ⟪ 𝓛ᵈℕ ⟫ δ
                    ≡⟨ refl ⟩
                 ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (succ n)
                     ≡⟨ ap (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) (q ⁻¹) ⟩
                 ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                    ∎
         where
          e₁ : is-defined (α i)
          e₁ = ≡-to-is-defined (ap (⦅ifZero⦆₀ a (α i)) q)
               (≡-to-is-defined (♯-on-total-element (⦅ifZero⦆₀ a (α i)) {l} e) d)
        q₀ = ♯-on-total-element (⦅ifZero⦆₀ a (α i)) e
        q₁ = ℕ-cases {𝓣 ⁺} {λ (n : ℕ) → ⦅ifZero⦆₀ a (α i) n
                                      ≡ ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) n} (value l e) p₀ pₛ
        q₂ = (♯-on-total-element (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) {l} e) ⁻¹
      v : (f : DCPO⊥[ 𝓛ᵈℕ , 𝓛ᵈℕ ])
        → ((i : I) → ⦅ifZero⦆₁ a (α i) ⊑⟨ ⟪ 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ⟫ ⟩ f)
        → (l : 𝓛 ℕ) (d : is-defined (((⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) ♯) l))
        → ((⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) ♯) l ≡ pr₁ f l
      v f ineqs l d = ((⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) ♯) l
                        ≡⟨ ♯-on-total-element (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) {l} e ⟩
                      ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                        ≡⟨ ℕ-cases {𝓣 ⁺} {λ (n : ℕ) → ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) n
                                                    ≡ pr₁ f l}
                            (value l e) p₀ pₛ ⟩
                      pr₁ f l
                        ∎
       where
        e : is-defined l
        e = ♯-is-defined (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) l d
        p₀ : value l e ≡ zero → ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e) ≡ pr₁ f l
        p₀ q = ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                  ≡⟨ ap (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) q ⟩
               ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) zero
                  ≡⟨ refl ⟩
               a
                  ≡⟨ r ⟩
               pr₁ f l
                  ∎
         where
          r : a ≡ pr₁ f l
          r = ∥∥-rec (lifting-of-set-is-a-set ℕ-is-set)
               h (is-Directed-gives-inhabited ⟪ 𝓛ᵈℕ ⟫ α δ)
           where
            h : (i : I) → a ≡ pr₁ f l
            h i = a                         ≡⟨ g ⟩
                  ((⦅ifZero⦆₀ a (α i)) ♯) l ≡⟨ ineqs i l e₀ ⟩
                  pr₁ f l                   ∎
             where
              g = a
                     ≡⟨ refl ⟩
                  ⦅ifZero⦆₀ a (α i) zero
                     ≡⟨ ap (⦅ifZero⦆₀ a (α i)) (q ⁻¹) ⟩
                  ⦅ifZero⦆₀ a (α i) (value l e)
                     ≡⟨ (♯-on-total-element (⦅ifZero⦆₀ a (α i)) e) ⁻¹ ⟩
                  ((⦅ifZero⦆₀ a (α i)) ♯) l
                     ∎
              e₀ : is-defined (((⦅ifZero⦆₀ a (α i)) ♯) l)
              e₀ = ≡-to-is-defined (g' ∙ g) d
               where
                g' = ((⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) ♯) l
                         ≡⟨ ♯-on-total-element (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) {l} e ⟩
                     ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                         ≡⟨ ap (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) q ⟩
                     ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) zero
                         ≡⟨ refl ⟩
                     a
                         ∎

        pₛ : (m : ℕ) → value l e ≡ succ m → ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                                          ≡ pr₁ f l
        pₛ m q = ∥∥-rec (lifting-of-set-is-a-set ℕ-is-set) h eₛ
         where
          g : (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) ♯) l ≡ ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
          g = ♯-on-total-element (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) {l} e
          g' = ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                  ≡⟨ ap (⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ)) q ⟩
               ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (succ m)
                  ≡⟨ refl ⟩
              ∐ ⟪ 𝓛ᵈℕ ⟫ δ
                  ∎
          eₛ : is-defined (∐ ⟪ 𝓛ᵈℕ ⟫ δ)
          eₛ = ≡-to-is-defined (g ∙ g') d
          h : (Σ i ꞉ I , is-defined (α i))
            → ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e) ≡ pr₁ f l
          h (i , dᵢ) = ⦅ifZero⦆₀ a (∐ ⟪ 𝓛ᵈℕ ⟫ δ) (value l e)
                          ≡⟨ g' ⟩
                       ∐ ⟪ 𝓛ᵈℕ ⟫ δ
                          ≡⟨ (family-defined-somewhere-sup-≡ ℕ-is-set δ i dᵢ) ⁻¹ ⟩
                       α i
                          ≡⟨ h' ⟩
                       ((⦅ifZero⦆₀ a (α i)) ♯) l
                          ≡⟨ ineqs i l (≡-to-is-defined h' dᵢ) ⟩
                       pr₁ f l
                          ∎
           where
            h' = α i
                    ≡⟨ refl ⟩
                 ⦅ifZero⦆₀ a (α i) (succ m)
                    ≡⟨ ap (⦅ifZero⦆₀ a (α i)) (q ⁻¹) ⟩
                 ⦅ifZero⦆₀ a (α i) (value l e)
                    ≡⟨ (♯-on-total-element (⦅ifZero⦆₀ a (α i)) {l} e) ⁻¹ ⟩
                 ((⦅ifZero⦆₀ a (α i)) ♯) l
                    ∎

  ⦅ifZero⦆ : DCPO⊥[ 𝓛ᵈℕ , 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ  ]
  ⦅ifZero⦆ = ⦅ifZero⦆₂ , c
   where
    c : is-continuous ⟪ 𝓛ᵈℕ ⟫ ⟪ 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ⟫ ⦅ifZero⦆₂
    c I α δ = u , v
     where
      u : (i : I) (b : 𝓛 ℕ) (l : 𝓛 ℕ) (d : is-defined (((⦅ifZero⦆₀ (α i) b) ♯) l))
        → ((⦅ifZero⦆₀ (α i) b) ♯) l ≡ ((⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) ♯) l
      u i b l d = ((⦅ifZero⦆₀ (α i) b) ♯) l
                     ≡⟨ ♯-on-total-element (⦅ifZero⦆₀ (α i) b) e ⟩
                  ⦅ifZero⦆₀ (α i) b (value l e)
                     ≡⟨ ℕ-cases {𝓣 ⁺} {λ (n : ℕ) →  ⦅ifZero⦆₀ (α i) b n
                                                 ≡ ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b n}
                          (value l e) p₀ pₛ ⟩
                  ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                     ≡⟨ (♯-on-total-element (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) {l} e) ⁻¹ ⟩
                  ((⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) ♯) l
                     ∎
       where
        e : is-defined l
        e = ♯-is-defined (⦅ifZero⦆₀ (α i) b) l d
        p₀ : value l e ≡ zero → ⦅ifZero⦆₀ (α i) b (value l e)
                              ≡ ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
        p₀ q = ⦅ifZero⦆₀ (α i) b (value l e)
                  ≡⟨ ap (⦅ifZero⦆₀ (α i) b) q ⟩
               ⦅ifZero⦆₀ (α i) b zero
                  ≡⟨ refl ⟩
               α i
                  ≡⟨ family-defined-somewhere-sup-≡ ℕ-is-set δ i e₁ ⟩
               ∐ ⟪ 𝓛ᵈℕ ⟫ δ
                  ≡⟨ refl ⟩
               ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b zero
                  ≡⟨ ap (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) (q ⁻¹) ⟩
               ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                  ∎
         where
          e₁ : is-defined (α i)
          e₁ = ≡-to-is-defined (ap (⦅ifZero⦆₀ (α i) b) q)
               (≡-to-is-defined (♯-on-total-element (⦅ifZero⦆₀ (α i) b) {l} e) d)
        pₛ : (n : ℕ) → value l e ≡ succ n → ⦅ifZero⦆₀ (α i) b (value l e)
                                          ≡ ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
        pₛ n q = ⦅ifZero⦆₀ (α i) b (value l e)
                    ≡⟨ ap (⦅ifZero⦆₀ (α i) b) q ⟩
                 ⦅ifZero⦆₀ (α i) b (succ n)
                    ≡⟨ refl ⟩
                 b
                    ≡⟨ refl ⟩
                 ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (succ n)
                    ≡⟨ ap (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) (q ⁻¹) ⟩
                 ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                    ∎

      v : (f : DCPO⊥[ 𝓛ᵈℕ , 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ])
        → ((i : I) (b : 𝓛 ℕ) → ⦅ifZero⦆₁ (α i) b ⊑⟨ ⟪ 𝓛ᵈℕ ⟹ᵈᶜᵖᵒ⊥ 𝓛ᵈℕ ⟫ ⟩ pr₁ f b)
        → (b : 𝓛 ℕ) (l : 𝓛 ℕ) (d : is-defined (((⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) ♯) l))
        → ((⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) ♯) l ≡ pr₁ (pr₁ f b) l
      v f ineqs b l d = ((⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) ♯) l
                           ≡⟨ ♯-on-total-element (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) {l} e ⟩
                        ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                           ≡⟨ ℕ-cases {𝓣 ⁺} {λ (n : ℕ) →  ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b n
                                                       ≡ pr₁ (pr₁ f b) l}
                                (value l e) p₀ pₛ ⟩
                        pr₁ (pr₁ f b) l
                           ∎
       where
        e : is-defined l
        e = ♯-is-defined (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) l d
        p₀ : value l e ≡ zero → ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e) ≡ pr₁ (pr₁ f b) l
        p₀ q = ∥∥-rec (lifting-of-set-is-a-set ℕ-is-set) h e₀
         where
          g : (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b ♯) l ≡ ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
          g = ♯-on-total-element (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) {l} e
          g' = ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e) ≡⟨ ap (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) q ⟩
               ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b zero        ≡⟨ refl ⟩
               ∐ ⟪ 𝓛ᵈℕ ⟫ δ                           ∎
          e₀ : is-defined (∐ ⟪ 𝓛ᵈℕ ⟫ δ)
          e₀ = ≡-to-is-defined (g ∙ g') d
          h : (Σ i ꞉ I , is-defined (α i))
            → ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e) ≡ pr₁ (pr₁ f b) l
          h (i , dᵢ) = ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                          ≡⟨ g' ⟩
                       ∐ ⟪ 𝓛ᵈℕ ⟫ δ
                          ≡⟨ (family-defined-somewhere-sup-≡ ℕ-is-set δ i dᵢ) ⁻¹ ⟩
                       α i
                          ≡⟨ h' ⟩
                       ((⦅ifZero⦆₀ (α i) b) ♯) l
                          ≡⟨ ineqs i b l (≡-to-is-defined h' dᵢ) ⟩
                       pr₁ (pr₁ f b) l
                          ∎
           where
            h' = α i
                    ≡⟨ refl ⟩
                 ⦅ifZero⦆₀ (α i) b zero
                    ≡⟨ ap (⦅ifZero⦆₀ (α i) b) (q ⁻¹) ⟩
                 ⦅ifZero⦆₀ (α i) b (value l e)
                    ≡⟨ (♯-on-total-element (⦅ifZero⦆₀ (α i) b) {l} e) ⁻¹ ⟩
                 ((⦅ifZero⦆₀ (α i) b) ♯) l
                    ∎

        pₛ : (m : ℕ) → value l e ≡ succ m → ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                                          ≡ pr₁ (pr₁ f b) l
        pₛ m q = ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                    ≡⟨ ap (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) q ⟩
                 ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (succ m)
                    ≡⟨ refl ⟩
                 b
                    ≡⟨ r ⟩
                 pr₁ (pr₁ f b) l
                    ∎
         where
          r : b ≡ pr₁ (pr₁ f b) l
          r = ∥∥-rec (lifting-of-set-is-a-set ℕ-is-set) h
               (is-Directed-gives-inhabited ⟪ 𝓛ᵈℕ ⟫ α δ)
           where
            h : (i : I) → b ≡ pr₁ (pr₁ f b) l
            h i = b                         ≡⟨ g ⟩
                  ((⦅ifZero⦆₀ (α i) b) ♯) l ≡⟨ ineqs i b l eₛ ⟩
                  pr₁ (pr₁ f b) l          ∎
             where
              g = b
                     ≡⟨ refl ⟩
                  ⦅ifZero⦆₀ (α i) b (succ m)
                     ≡⟨ ap (⦅ifZero⦆₀ (α i) b) (q ⁻¹) ⟩
                  ⦅ifZero⦆₀ (α i) b (value l e)
                     ≡⟨ (♯-on-total-element (⦅ifZero⦆₀ (α i) b) e) ⁻¹ ⟩
                  ((⦅ifZero⦆₀ (α i) b) ♯) l
                     ∎
              eₛ : is-defined (((⦅ifZero⦆₀ (α i) b) ♯) l)
              eₛ = ≡-to-is-defined (g' ∙ g) d
               where
                g' = ((⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) ♯) l
                        ≡⟨ ♯-on-total-element (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) {l} e ⟩
                     ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (value l e)
                        ≡⟨ ap (⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b) q ⟩
                     ⦅ifZero⦆₀ (∐ ⟪ 𝓛ᵈℕ ⟫ δ) b (succ m)
                        ≡⟨ refl ⟩
                     b
                        ∎

\end{code}
