\documentclass[main.tex]{subfiles}
\begin{document}
\AgdaNoSpaceAroundCode{
\begin{code}[hide]

open import UF-PropTrunc
open import SpartanMLTT

module ScottModel
        (pt : propositional-truncations-exist)
        (fe : âˆ€ {ğ“¤ ğ“¥} â†’ funext ğ“¤ ğ“¥)
        (pe : propext ğ“¤â‚€)
       where

open PropositionalTruncation pt
open import PCF-With-Lambda pt
open import Dcpo pt fe ğ“¤â‚€ renaming (constant-functions-are-continuous to const-functions-are-continuous)
open import DcpoConstructions pt fe
open DcpoConstructionsGeneral ğ“¤â‚€
open LiftingDcpo ğ“¤â‚€ pe

open import DcpoProducts pt fe 
open DcpoProductsGeneral ğ“¤â‚€ hiding (to-Ã—-DCPO ; to-Ã—-DCPOâŠ¥)
open import DcpoProducts-Curry pt fe ğ“¤â‚€ hiding (eval)

open import Dcpo-IfZero pt fe pe hiding (â¦…ifZeroâ¦†-arguments ; â¦…ifZeroâ¦†Î“ ; â¦…ifZeroâ¦†-uncurried' ; â¦…ifZeroâ¦†-uncurried)
open import Dcpo-Contexts pt fe pe hiding ( âŠ¤áµˆá¶œáµ–áµ’ ; âŠ¤áµˆá¶œáµ–áµ’âŠ¥ ;ã€_ã€‘ ; var-DCPO ; var-DCPOâŠ¥ ; extract ; âˆ˜-of-prâ‚“-is-continuous )
open import Dcpo-FunctionComposition pt fe ğ“¤â‚€
open import DcpoProducts-Continuity pt fe ğ“¤â‚€

open import NaturalNumbers-Properties
open import UF-Miscelanea

open import Lifting ğ“¤â‚€
open import LiftingMonad ğ“¤â‚€ hiding (Î¼)

\end{code}
}
\section{Scott Model of PCF}
\label{scottmodel}
With our constructions in domain theory, we now have enough to define the Scott model of PCF \cite{Scott1993}.
\subsection{Types}
We define the following function from PCF types to their denotational interpretation:
\begin{code}
âŸ¦_âŸ§ : type â†’ DCPOâŠ¥ {ğ“¤â‚} {ğ“¤â‚}
\end{code}
We have two cases we need to provide an interpretation for. The first is the base type, and the second is the function type.
\subsubsection*{The base type}
We first construct the representation of our base type \AgdaInductiveConstructor{Î¹}. We follow our intuitions from \cref{domaintheory} that we represent \AgdaInductiveConstructor{Î¹} as a DCPO, where the underlying set is $â„•\cupâŠ¥$, with the information ordering.

In our construction, to embed the notion of an undefined element into our set, we use the lifting monad as Tom de Jong does \cite{jong2019scott}. The lifting monad, from a type such as $â„•$, construct a new type $ğ“›\ â„•$, called the ``lifting'' of $â„•$. The lifting of $â„•$ gives us a way to express the definedness of elements, allowing us to say that all elements from the original type $â„•$ are ``total'' (defined), and there is an undefined element $âŠ¥$.

The importance of using the lifting monad is it gives us a constructive way to represent the definedness of elements, and from this we can proceed to define an order which is compatible with our developments:
\begin{center}
$x \mathbin{âŠ‘} y = \operatorname{is-defined} x â†’ x â‰¡ y$
\end{center}
This order follows our intuition that for flat domains, $âŠ¥$ is less than all other elements, and total elements are incomparable.

It also follows that a lifted type $ğ“›\ Y$ forms a DCPO under the above ordering if the type $Y$ is a set, as shown by Knapp and Escard\'o \cite{escardoknapppartial}. This construction of a DCPO from a lifted type has been developed in Agda by Tom de Jong. We shall be making use of \AgdaFunction{ğ“›-DCPOâŠ¥}, which, given a set $Y$, will produce a DCPO with least element $âŠ¥$ under the mentioned ordering, where the underlying set is the lifting of $Y$. We can produce the total elements of the lifting of $Y$ in Agda from an element \AgdaBound{n} of the type $Y$ as \AgdaFunction{Î·}\AgdaSpace{}\AgdaBound{n}, and there is always the undefined element \AgdaFunction{âŠ¥} as previously discussed.

We can now construct the interpretation of the base type \AgdaInductiveConstructor{Î¹}. We use the fact that \AgdaDatatype{â„•} is a set from Mart\'in Escard\'o's developments. For shorthand, we will use \AgdaFunction{ğ“›áµˆâ„•} to refer to the DCPO of the lifted natural numbers. 
\begin{code}
âŸ¦ Î¹ âŸ§     = ğ“›áµˆâ„•
\end{code}
\subsubsection*{Function types}
We then represent function type \AgdaPCFType{Ïƒ}{Ï„} as a DCPO with $âŠ¥$ where the underlying set is continuous functions from the interpretation of \AgdaBound{Ïƒ} and the interpretation of \AgdaBound{Ï„}.
\begin{code}
âŸ¦ Ïƒ â‡’ Ï„ âŸ§ = âŸ¦ Ïƒ âŸ§ âŸ¹áµˆá¶œáµ–áµ’âŠ¥ âŸ¦ Ï„ âŸ§
\end{code}

\subsection{Contexts}
We interpret contexts simply as a product between the interpretation of each type within the context. 

The first consideration is to develop a representation for the empty context. We form a DCPO where the underlying set is \AgdaDatatype{ğŸ™} - the type with one element, namely \AgdaInductiveConstructor{*}. We associate an order which states for any \AgdaBound{x} and \AgdaBound{y} of type \AgdaDatatype{ğŸ™}, we have \AgdaBound{x}\AgdaSpace{}\AgdaFunction{âŠ‘âŸ¨âŠ¤âŸ©}\AgdaSpace{}\AgdaBound{y}. The set \AgdaDatatype{ğŸ™} and this order form a DCPO, which we name in Agda as \AgdaType{\AgdaFunction{âŠ¤áµˆá¶œáµ–áµ’}}{\AgdaFunction{DCPO}\AgdaSpace{}\AgdaSymbol{\{}\AgdaFunction{ğ“¤â‚}\AgdaSymbol{\}}\AgdaSpace{}\AgdaSymbol{\{}\AgdaFunction{ğ“¤â‚}\AgdaSymbol{\}}}. We omit the trivial definition and proof of DCPO axioms for brevity. It is also trivial to show this DCPO contains a least element, namely \AgdaInductiveConstructor{*}, which we show in Agda in the proof \AgdaType{\AgdaFunction{âŠ¤áµˆá¶œáµ–áµ’âŠ¥}}{\AgdaFunction{DCPOâŠ¥}\AgdaSpace{}\AgdaSymbol{\{}\AgdaFunction{ğ“¤â‚}\AgdaSymbol{\}}\AgdaSpace{}\AgdaSymbol{\{}\AgdaFunction{ğ“¤â‚}\AgdaSymbol{\}}}.
\hide{
\begin{code}[hide]
âŠ¤áµˆá¶œáµ–áµ’ : DCPO {ğ“¤â‚} {ğ“¤â‚}
âŠ¤áµˆá¶œáµ–áµ’ = ğŸ™ , _âŠ‘âŸ¨âŠ¤âŸ©_ , s , p , r , t , a , d
  where
    _âŠ‘âŸ¨âŠ¤âŸ©_ : ğŸ™ {ğ“¤â‚} â†’ ğŸ™ {ğ“¤â‚} â†’ ğ“¤â‚ Ì‡
    x âŠ‘âŸ¨âŠ¤âŸ© y = ğŸ™
    s : is-set ğŸ™
    s = props-are-sets ğŸ™-is-prop
    p : is-prop-valued {ğ“¤â‚} {ğ“¤â‚} {ğŸ™} (Î» x y â†’ ğŸ™)
    p _ _ * * = refl
    r : is-reflexive _âŠ‘âŸ¨âŠ¤âŸ©_
    r _ = *
    t : is-transitive {ğ“¤â‚} {ğ“¤â‚} {ğŸ™} (Î» x y â†’ ğŸ™)
    t _ _ _ _ _ = *
    a : âˆ€ (x y : ğŸ™) â†’ x âŠ‘âŸ¨âŠ¤âŸ© y â†’ y âŠ‘âŸ¨âŠ¤âŸ© x â†’ x â‰¡ y
    a * * _ _ = refl
    d : is-directed-complete (Î» x y â†’ ğŸ™)
    d _ _ _ = * , ((Î» _ â†’ *) , (Î» _ _ â†’ *))
\end{code}
Then, the proof that this forms a DCPO with bottom is trivial. We only have one possible element that can be our least element, the sole inhabitant of \AgdaFunction{ğŸ™}. Since our order is just defined as \AgdaFunction{ğŸ™} for any \AgdaBound{x} and \AgdaBound{y}, a proof that \AgdaInductiveConstructor{*} is less than any other element of the underlying set \AgdaFunction{ğŸ™} is just the sole element of \AgdaFunction{ğŸ™}, \AgdaInductiveConstructor{*}. 
\begin{code}
Táµˆá¶œáµ–áµ’âŠ¥ : DCPOâŠ¥ {ğ“¤â‚} {ğ“¤â‚}
Táµˆá¶œáµ–áµ’âŠ¥ = âŠ¤áµˆá¶œáµ–áµ’ , (* , Î» _ â†’ *)
\end{code}
}

We now can inductively define our interpretation of a context. For our inductive case where we have a context \AgdaExtendContext{Î“}{x}, we make use of \AgdaFunction{Ã—áµˆá¶œáµ–áµ’âŠ¥} to form the product between the interpretation of \AgdaBound{Î“} and the interpretation of the type \AgdaBound{x}.
\begin{code}
ã€_ã€‘ : {n : â„•} (Î“ : Context n) â†’ DCPOâŠ¥ {ğ“¤â‚} {ğ“¤â‚}
ã€ âŸ¨âŸ© ã€‘ = Táµˆá¶œáµ–áµ’âŠ¥
ã€ Î“ â€™ x ã€‘ = ã€ Î“ ã€‘ Ã—áµˆá¶œáµ–áµ’âŠ¥ âŸ¦ x âŸ§
\end{code}
From this definition, we can think of \AgdaBound{d}\AgdaSpace{}\AgdaSymbol{:}\AgdaSpace{}\AgdaFunction{âŸ¨}\AgdaSpace{}\AgdaFunction{âŸª}\AgdaSpace{}\AgdaFunction{ã€}\AgdaSpace{}\AgdaBound{Î“}\AgdaSpace{}\AgdaFunction{ã€‘}\AgdaSpace{}\AgdaFunction{âŸ«}\AgdaSpace{}\AgdaFunction{âŸ©} as representing a list of values for our variables in the context \AgdaBound{Î“} to be substituted with in our denotational interpretation.

\subsection{Terms}
We next inductively define the interpretation of terms. Since terms may contain free variables, we interpret a term of type \AgdaBound{Ïƒ} under a given context \AgdaBound{Î“} as a continuous function from the interpretation of the context to the interpretation of \AgdaBound{Ïƒ}. Intuitively, this means that given a list of values of the types listed in \AgdaBound{Î“}, we can produce a closed term belonging to the type \AgdaBound{Ïƒ}. We begin by first developing some constructs which we will use in our interpretation function.

\subsubsection*{Interpreting variables}
When interpreting the term \AgdaVar{i} which has the type $Ïƒ$ under a context \AgdaBound{Î“}, it makes sense that this is essentially the $i$-th projection of an inhabitant of \AgdaExtractSet{\AgdaExtractDCPO{\AgdaContextInterp{Î“}}}. As such, we can see this as a continuous function between \AgdaContextInterp{Î“} and \AgdaTypeInterp{Ïƒ}.

We implement this $i$-th projection in Agda by induction on the lookup judgement. We take the second projection after taking $i$ first projections, to extract the desired value.
\begin{code}
extract : {n : â„•} {Ïƒ : type} {Î“ : Context n} â†’ (x : Î“ âˆ‹ Ïƒ) â†’ âŸ¨ âŸª ã€ Î“ ã€‘ âŸ« âŸ©  â†’ âŸ¨ âŸª âŸ¦ Ïƒ âŸ§ âŸ« âŸ©
extract Z (_ , a) = a
extract (S x) (d , _) = extract x d
\end{code}
Since in our setting we require that functions are continuous, we need a proof of continuity. For any variable \AgdaVar{x}, we will want our underlying function to be \AgdaFunction{extract}\AgdaSpace{}\AgdaBound{x}. As a result we need to show that for any lookup judgement \AgdaBound{x}, \AgdaFunction{extract}\AgdaSpace{}\AgdaBound{x} is a continuous function. We show this is the case by induction on the lookup judgement itself, as this determines the structure of \AgdaFunction{extract}\AgdaSpace{}\AgdaBound{x}. The first case is when the lookup judgement is \AgdaInductiveConstructor{Z}, and we can provide our value. In this case, our function is just \AgdaField{prâ‚‚}, so we provide a proof that \AgdaField{prâ‚‚} is continuous.
\begin{code}
extract-is-continuous : {n : â„•} {Î“ : Context n} {Ïƒ : type} (x : Î“ âˆ‹ Ïƒ)
                        â†’ is-continuous âŸª ã€ Î“ ã€‘ âŸ« âŸª âŸ¦ Ïƒ âŸ§ âŸ« (extract x)
extract-is-continuous {_} {Î“ â€™ Ïƒ} {Ïƒ} Z = continuity-of-function âŸª ã€ Î“ â€™ Ïƒ ã€‘ âŸ« âŸª âŸ¦ Ïƒ âŸ§ âŸ«
                                                        (prâ‚‚-is-continuous âŸª ã€ Î“ ã€‘ âŸ« âŸª âŸ¦ Ïƒ âŸ§ âŸ«)
\end{code}
In the inductive case, we have a context \AgdaExtendContext{Î“}{Ï„}, and a proof \AgdaLookupJudge{x}{Î“}{Ïƒ}.
\AgdaFunction{extract}\AgdaSpace{}\AgdaBrackets{\AgdaInductiveConstructor{S}\AgdaSpace{}\AgdaBound{x}} is the same as \AgdaFunction{extract}\AgdaSpace{}\AgdaBound{x}\AgdaSpace{}\AgdaFunction{âˆ˜}\AgdaSpace{}\AgdaField{prâ‚}. Since we have a proof \AgdaFunction{âˆ˜áµˆá¶œáµ–áµ’} that the composition of continuous functions produces a continuous function, we just need to prove that \AgdaFunction{extract}\AgdaSpace{}\AgdaBound{x} is a continuous function, and that \AgdaField{prâ‚} is continuous. The fact that \AgdaFunction{extract}\AgdaSpace{}\AgdaBound{x} is continuous is provided by our inductive hypothesis, and \AgdaField{prâ‚} is known to be continuous. As such, the continuity property of the composition of these continuous functions is enough to complete the proof.
\begin{code}
extract-is-continuous {_} {Î“ â€™ Ï„} {Ïƒ} (S x)
                   = continuity-of-function âŸª ã€ Î“ â€™ Ï„ ã€‘ âŸ« âŸª âŸ¦ Ïƒ âŸ§ âŸ« ( [ âŸª ã€ Î“ â€™ Ï„ ã€‘ âŸ« , âŸª ã€ Î“ ã€‘ âŸ« , âŸª âŸ¦ Ïƒ âŸ§ âŸ« ]
                                 extract x , (extract-is-continuous x) âˆ˜áµˆá¶œáµ–áµ’ prâ‚-is-continuous âŸª ã€ Î“ ã€‘ âŸ« âŸª âŸ¦ Ï„ âŸ§ âŸ«)
\end{code}
We then provide a function which, for any lookup judgement, provides a continuous function which extracts the desired value from a context.
\begin{code}
var-extract : {n : â„•} {Ïƒ : type} {Î“ : Context n} â†’ (x : Î“ âˆ‹ Ïƒ) â†’ DCPO[ âŸª ã€ Î“ ã€‘ âŸ« , âŸª âŸ¦ Ïƒ âŸ§ âŸ« ]
var-extract x = extract x , extract-is-continuous x
\end{code}
\subsubsection*{Interpreting IfZero} 
In his implementation of the Scott model of a combinatorial version of PCF, Tom de Jong has developed an interpretation of \AgdaInductiveConstructor{IfZero} in his setting. He defined the interpretation using the Kleisli extension as $\lambda x,y.\pa{\chi_{x,y}}^{\#}$, where:
    \[
      \chi_{x,y}(n) \colonequiv
      \begin{cases}
        x &\text{if } n = 0; \\
        y &\text{else};
      \end{cases}
    \]
However, since our implementation of PCF has contexts, we cannot just use Tom de Jong's interpretation for our \AgdaInductiveConstructor{IfZero}. Instead, we construct a way to first evaluate our terms \AgdaBound{t}, \AgdaBound{tâ‚}, and \AgdaBound{tâ‚‚} which make up a term \AgdaIfZero{t}{tâ‚}{tâ‚‚} under a context \AgdaBound{Î“}.
\begin{code}[hide]
module _ (ğ““ : DCPO {ğ“¤} {ğ“¤'})
        (ğ“” : DCPO {ğ“£} {ğ“£'})
        (ğ“• : DCPO {ğ“¦} {ğ“¦'})
    where
\end{code}

We first define a continuous function to apply a list of values for a context to two terms at once. We generalise this to constructing a new continuous function from two continuous functions of the same input domain. We omit the proof of continuity for brevity.
\begin{code}
  to-Ã—-DCPO : DCPO[ ğ““ , ğ“” ] â†’  DCPO[ ğ““ , ğ“• ] â†’ DCPO[ ğ““ , ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“• ]
  to-Ã—-DCPO f g = h , c
    where
      h : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“• âŸ©
      h d = (underlying-function ğ““ ğ“” f d) , (underlying-function ğ““ ğ“• g d)
      c : is-continuous ğ““ (ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“•) h
      c I Î± Î´ = u , s -- Proofs of u and s omitted for brevity
\end{code}
\begin{code}[hide]
        where
          h-is-monotone : is-monotone ğ““ (ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“•) h
          h-is-monotone x y p = (continuous-functions-are-monotone ğ““ ğ“” f x y p)
                                    , (continuous-functions-are-monotone ğ““ ğ“• g x y p)
          u : is-upperbound (underlying-order (ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“•)) (h (âˆ ğ““ Î´)) (Î» i â†’ h (Î± i))
          u i = h-is-monotone (Î± i) (âˆ ğ““ Î´) (âˆ-is-upperbound ğ““ Î´ i)
          s : (uâ‚ : âŸ¨ ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“• âŸ©) â†’
                ((i : I) â†’ underlying-order (ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“•) (h (Î± i)) uâ‚) â†’
                underlying-order (ğ“” Ã—áµˆá¶œáµ–áµ’ ğ“•) (h (âˆ ğ““ Î´)) uâ‚
          s (uâ‚ , uâ‚‚) p = eâ‚ , eâ‚‚
            where
              eâ‚ : underlying-function ğ““ ğ“” f (âˆ ğ““ Î´) âŠ‘âŸ¨ ğ“” âŸ© uâ‚
              eâ‚ = transport (Î» - â†’ - âŠ‘âŸ¨ ğ“” âŸ© uâ‚) (pâ‚ â»Â¹) eâ‚ƒ
                where
                  pâ‚ : underlying-function ğ““ ğ“” f (âˆ ğ““ Î´) â‰¡ âˆ ğ“” (image-is-directed ğ““ ğ“” f Î´)
                  pâ‚ = continuous-function-âˆ-â‰¡ ğ““ ğ“” f Î´
                  eâ‚ƒ : âˆ ğ“” (image-is-directed ğ““ ğ“” f Î´) âŠ‘âŸ¨ ğ“” âŸ© uâ‚
                  eâ‚ƒ = âˆ-is-lowerbound-of-upperbounds ğ“” (image-is-directed ğ““ ğ“” f Î´) uâ‚ (Î» i â†’ prâ‚ (p i))
              eâ‚‚ : underlying-function ğ““ ğ“• g (âˆ ğ““ Î´) âŠ‘âŸ¨ ğ“• âŸ© uâ‚‚
              eâ‚‚ = transport (Î» - â†’ - âŠ‘âŸ¨ ğ“• âŸ© uâ‚‚) (pâ‚ â»Â¹) eâ‚ƒ
                where
                  pâ‚ : underlying-function ğ““ ğ“• g (âˆ ğ““ Î´) â‰¡ âˆ ğ“• (image-is-directed ğ““ ğ“• g Î´)
                  pâ‚ = continuous-function-âˆ-â‰¡ ğ““ ğ“• g Î´
                  eâ‚ƒ : âˆ ğ“• (image-is-directed ğ““ ğ“• g Î´) âŠ‘âŸ¨ ğ“• âŸ© uâ‚‚
                  eâ‚ƒ = âˆ-is-lowerbound-of-upperbounds ğ“• (image-is-directed ğ““ ğ“• g Î´) uâ‚‚ (Î» i â†’ prâ‚‚ (p i))
\end{code}
\begin{code}[hide]
module _ (ğ““ : DCPOâŠ¥ {ğ“¤} {ğ“¤'})
        (ğ“” : DCPOâŠ¥ {ğ“£} {ğ“£'})
        (ğ“• : DCPOâŠ¥ {ğ“¦} {ğ“¦'})
    where
\end{code}
We then, for convenience, define this for continuous functions on DCPOs with least elements.
\begin{code}
  to-Ã—-DCPOâŠ¥ : DCPOâŠ¥[ ğ““ , ğ“” ] â†’ DCPOâŠ¥[ ğ““ , ğ“• ] â†’ DCPOâŠ¥[ ğ““ , ğ“” Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“• ]
  to-Ã—-DCPOâŠ¥ f g = to-Ã—-DCPO âŸª ğ““ âŸ« âŸª ğ“” âŸ« âŸª ğ“• âŸ« f g
\end{code}
\begin{code}[hide]
module _ {a : â„•} (Î“ : Context a)
        where
\end{code}
Since we have three terms which we want to evaluate under the same context, we can then use \AgdaFunction{to-Ã—-DCPOâŠ¥} to construct a continuous function that can perform the denotational substitution of a list of values for the variables in the context to take on. This is just two applications of \AgdaFunction{to-Ã—-DCPOâŠ¥}. The first to form a continuous function that evaluates two terms under the same context, and the second application to form a continuous function which evaluates the original two terms and a third term under the same context. We construct this in such a manner that we end up with the result being a pair of the form $((a , b) , c)$ as opposed to $(a , (b , c))$. This will prevent us from having to use a proof of associativity of the product of DCPOs, which will become apparent later. 
\begin{code}
  â¦…ifZeroâ¦†-arguments : DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• ] â†’ DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• ]
                   â†’ DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• ] â†’ DCPOâŠ¥[ ã€ Î“ ã€‘ , (ğ“›áµˆâ„• Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„•) Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„• ]
  â¦…ifZeroâ¦†-arguments a b c = to-Ã—-DCPOâŠ¥ ã€ Î“ ã€‘ (ğ“›áµˆâ„• Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„•) ğ“›áµˆâ„• f c
        where
          f : DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„• ]
          f = to-Ã—-DCPOâŠ¥ ã€ Î“ ã€‘ ğ“›áµˆâ„• ğ“›áµˆâ„• a b
\end{code}
Now we have a way to evaluate all three of our terms under the same context, we next look at how we can make use of Tom de Jong's interpretation of \AgdaInductiveConstructor{IfZero} from his combinatorial setting. The type of this interpretation is \AgdaConFunBot{\AgdaFunction{ğ“›áµˆâ„•}}{\AgdaFunction{ğ“›áµˆâ„•}\AgdaSpace{}\AgdaFunction{âŸ¹áµˆá¶œáµ–áµ’âŠ¥}\AgdaSpace{}\AgdaFunction{ğ“›áµˆâ„•}\AgdaSpace{}\AgdaFunction{âŸ¹áµˆá¶œáµ–áµ’âŠ¥}\AgdaSpace{}\AgdaFunction{ğ“›áµˆâ„•}}, and is named \AgdaFunction{â¦…ifZeroâ¦†}. We can not compose this directly with our function that evaluates our subterms of \AgdaInductiveConstructor{IfZero}, as the input of \AgdaFunction{â¦…ifZeroâ¦†} does not match the type of the output of \AgdaFunction{â¦…ifZeroâ¦†-arguments}. However, through the use of uncurry, we can construct a function from \AgdaFunction{â¦…ifZeroâ¦†} that takes the arguments as a product, as opposed to one at a time. We therefore construct \AgdaFunction{â¦…ifZeroâ¦†-uncurried} as follows:
\begin{code}
â¦…ifZeroâ¦†-uncurried' : DCPOâŠ¥[ ğ“›áµˆâ„• Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„• , ğ“›áµˆâ„• âŸ¹áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„• ]
â¦…ifZeroâ¦†-uncurried' = uncurryáµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„• ğ“›áµˆâ„• (ğ“›áµˆâ„• âŸ¹áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„•) â¦…ifZeroâ¦†

â¦…ifZeroâ¦†-uncurried : DCPOâŠ¥[ (ğ“›áµˆâ„• Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„•) Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„• , ğ“›áµˆâ„• ]
â¦…ifZeroâ¦†-uncurried = uncurryáµˆá¶œáµ–áµ’âŠ¥ (ğ“›áµˆâ„• Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„•) ğ“›áµˆâ„• ğ“›áµˆâ„• â¦…ifZeroâ¦†-uncurried'
\end{code}
Now we can see why the associativity mattered when we were constructing a pair, as since applying uncurry once formed a pair of the outermost arguments, then again formed a pair where the first projection is the pair of the outermost arguments, and the second projection is the innermost argument of \AgdaFunction{â¦…ifZeroâ¦†}. We require the associativity of our argument construction to be the same. The reason is, for types $A$, $B$, and $C$, whilst $(AÃ—B)Ã—C$ is isomorphic to $AÃ—(BÃ—C)$, they are clearly not the same type, and to access an element of type $C$ in the first product type would require a different chain of projections to the second product type.

We now can produce our interpretation of \AgdaInductiveConstructor{IfZero} in our setting. We compose the function which evaluates each of the subterms of \AgdaIfZero{a}{b}{c} under a context \AgdaBound{Î“} with the uncurried \AgdaFunction{â¦…ifZeroâ¦†} construction. This provides us with a continuous function that takes a list of values to substitute for the variables, and produces the desired result of the \AgdaInductiveConstructor{IfZero} evaluation.
\begin{code}[hide]
module _ {a : â„•} (Î“ : Context a)
        where
\end{code}
\begin{code}
  â¦…ifZeroâ¦†Î“ : DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• ] â†’ DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• ] â†’ DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• ]
            â†’ DCPOâŠ¥[ ã€ Î“ ã€‘ , ğ“›áµˆâ„• ]
  â¦…ifZeroâ¦†Î“ a b c = [ ã€ Î“ ã€‘ , (ğ“›áµˆâ„• Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„•) Ã—áµˆá¶œáµ–áµ’âŠ¥ ğ“›áµˆâ„• , ğ“›áµˆâ„• ]
                    â¦…ifZeroâ¦†-uncurried âˆ˜áµˆá¶œáµ–áµ’âŠ¥ (â¦…ifZeroâ¦†-arguments Î“ a b c)
\end{code}

\subsubsection*{Interpreting application}
\begin{code}[hide]
module _ (ğ““ : DCPO {ğ“¤} {ğ“¤'})
         (ğ“” : DCPO {ğ“£} {ğ“£'})
       where
\end{code}
For an interpretation of \AgdaApp{M}{N}, we apply the interpretation of \AgdaBound{M} to the interpretation of \AgdaBound{N}. We begin by constructing a continuous function called \AgdaFunction{eval} which performs this application given a continuous function from a DCPO $ğ““$ to $ğ“”$, and an element of the DCPO $ğ““$, corresponding to the evaluation map mentioned by Streicher \cite[Section~4]{domaintheoreticfoundations}.
\begin{AgdaAlign}
\begin{code}
  eval : DCPO[ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) Ã—áµˆá¶œáµ–áµ’ ğ““ , ğ“” ]
  eval = f , c
    where
      f : âŸ¨ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) Ã—áµˆá¶œáµ–áµ’ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©
      f (g , d) = underlying-function ğ““ ğ“” g d
\end{code}
\hide{
We first show that \AgdaFunction{f} is monotone, which is useful in our proof that \AgdaFunction{f} is continuous. We assume $(gâ‚ , dâ‚)$ and $(gâ‚‚ , dâ‚‚)$ as elements of the DCPO \AgdaBrackets{\AgdaBrackets{\FuncDcpo{ğ““}{ğ“”}}\AgdaSpace{}\AgdaFunction{Ã—áµˆá¶œáµ–áµ’}\AgdaSpace{}\AgdaBound{ğ““}}, and that $(gâ‚ , dâ‚)$ is less than $(gâ‚‚ , dâ‚‚)$. From this, we get that $gâ‚$ is less than $gâ‚‚$ and $dâ‚$ is less than $dâ‚‚$. We want to show that $f (gâ‚ , dâ‚)$ is less than $f (gâ‚‚ , dâ‚‚)$. Since continuous functions are monotone, and $gâ‚$ is a continuous function between the DCPOs $ğ““$ and $ğ“”$, we use the monotonicity of $gâ‚$ to show that $f (gâ‚ , dâ‚)$ is less than $f (gâ‚ , dâ‚‚)$. We then show that $f (gâ‚ , dâ‚‚)$ is less than $f (gâ‚‚ , dâ‚‚)$. This follows simply from the definition of the pointwise order, and since the continuous function $gâ‚$ is less than $gâ‚‚$, it follows that $f (gâ‚ , dâ‚‚)$ is less than $f (gâ‚‚ , dâ‚‚)$. The transitivity of the order of the DCPO $ğ“”$ concludes our proof. 
\begin{code}
      f-is-monotone : is-monotone ((ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) Ã—áµˆá¶œáµ–áµ’ ğ““) ğ“” f
      f-is-monotone (gâ‚ , dâ‚) (gâ‚‚ , dâ‚‚) (gâ‚âŠ‘gâ‚‚ , dâ‚âŠ‘dâ‚‚) =
                     f (gâ‚ , dâ‚) âŠ‘âŸ¨ ğ“” âŸ©[ continuous-functions-are-monotone ğ““ ğ“” gâ‚ dâ‚ dâ‚‚ dâ‚âŠ‘dâ‚‚ ]
                     f (gâ‚ , dâ‚‚) âŠ‘âŸ¨ ğ“” âŸ©[ gâ‚âŠ‘gâ‚‚ dâ‚‚ ]
                     f (gâ‚‚ , dâ‚‚) âˆâŸ¨ ğ“” âŸ©
\end{code}
}
We next provide a proof that \AgdaFunction{f} is continuous. Similarly to our uncurry proof, since our function input is a product, we can prove that \AgdaFunction{f} is continuous by showing it is continuous in each of its arguments. Since the proofs of \AgdaFunction{continuousâ‚} and \AgdaFunction{continuousâ‚‚} are relatively straightforward, we omit them for brevity.
\begin{code}
      c : is-continuous ((ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) Ã—áµˆá¶œáµ–áµ’ ğ““) ğ“” f
      c = continuous-in-argumentsâ†’continuous (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) ğ““ ğ“” f continuousâ‚ continuousâ‚‚
        where -- Proofs of continuity in arguments omitted.
\end{code}
\hide{
The proof that \AgdaFunction{f} is continuous in its first argument is short. For a fixed \AgdaBound{d}, we want to show that \AgdaFunction{f}\AgdaSpace{}\AgdaPair{\AgdaUpperBound{\AgdaBrackets{\FuncDcpo{ğ““}{ğ“”}}}{Î´}}{d} is the least upper bound of a directed family $Î» i â†’ \AgdaFunction{f} (Î± i , d)$, where \AgdaType{Î±}{\AgdaFun{I}{\AgdaExtractSet{\FuncDcpo{ğ““}{ğ“”}}}}, and $Î´$ is a proof that $Î±$ is directed. However, \AgdaFunction{f}\AgdaSpace{}\AgdaPair{\AgdaUpperBound{\AgdaBrackets{\FuncDcpo{ğ““}{ğ“”}}}{Î´}}{d} is the equal to the least upper bound of the pointwise family by definition, so our proof follows straight from the directed-completeness of the DCPO \AgdaBound{ğ“”}, which states that there exists a least upper bound for every directed family in DCPO \AgdaBound{ğ“”}, and that the pointwise family is a directed family in \AgdaBound{ğ“”}.
\begin{code}
          continuousâ‚ : (d : âŸ¨ ğ““ âŸ©) â†’ is-continuous (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) ğ“” (Î» g â†’ f (g , d))
          continuousâ‚ d I Î± Î´ = âˆ-is-sup ğ“” (pointwise-family-is-directed ğ““ ğ“” Î± Î´ d)
\end{code}
We next consider how to show that \AgdaFunction{f} is continuous in its second argument. We want to show that for a fixed \AgdaBound{g}, any directed family $Î± : I â†’ âŸ¨ ğ““ âŸ©$, where \AgdaBound{Î´} is a proof that $Î±$ is directed, \AgdaFunction{f}\AgdaSpace{}\AgdaPair{g}{\AgdaUpperBound{ğ““}{Î´}} is the least upper bound of the directed family $Î» i â†’ f (g , Î± i)$. We provide a proof \AgdaFunction{u} that it is first an upper bound, and a proof \AgdaFunction{l} that it is a least upper bound.
\begin{code}
          continuousâ‚‚ : (g : âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“” âŸ©) â†’ is-continuous ğ““ ğ“” (Î» d â†’ f (g , d))
          continuousâ‚‚ g I Î± Î´ = u , l
            where
\end{code}
The proof that $f (g , âˆ ğ““ Î´)$ is an upper bound follows trivially from the monotonicity of $f$. We want to show that for any $i$, $f (g , Î± i)$ is less than \AgdaFunction{f}\AgdaSpace{}\AgdaPair{g}{\AgdaUpperBound{ğ““}{Î´}}. We can construct a proof that $g$ is less than $g$ by reflexivity of the order, and that $Î± i$ is less than $âˆ ğ““ Î´$ by the definition of the least upper bound. From the definition of the component-wise ordering, it then follows that $(g , Î± i)$ is less than $(g , âˆ ğ““ Î´)$. From the monotonicity of $f$, it then follows that $f (g , Î± i)$ is less than \AgdaFunction{f}\AgdaSpace{}\AgdaPair{g}{\AgdaUpperBound{ğ““}{Î´}}.
\begin{code}
              u : is-upperbound (underlying-order ğ“”) (f (g , âˆ ğ““ Î´)) (Î» z â†’ f (g , Î± z))
              u i = f-is-monotone (g , Î± i) (g , âˆ ğ““ Î´) ((reflexivity (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) g) , (âˆ-is-upperbound ğ““ Î´ i))
\end{code}
We next want to show that for any other upper bound $uâ‚$ of $Î» i. f (g , Î± i)$, that $f (g , âˆ ğ““ Î´)$ is less than $uâ‚$. Since $f (g , âˆ ğ““ Î´)$ is defined as \UnderlyingFunction{ğ““}{ğ“”}{g}\AgdaBrackets{\AgdaSpace{}\AgdaUpperBound{ğ““}{Î´}}, we use the continuity of $g$ to show that $f (g , âˆ ğ““ Î´)$ is equal to \AgdaUpperBound{ğ“”}{\AgdaFunction{image-is-directed}\AgdaSpace{}\AgdaBound{ğ““}\AgdaSpace{}\AgdaBound{ğ“”}\AgdaSpace{}\AgdaBound{g}\AgdaSpace{}\AgdaBound{Î´}}, where \AgdaFunction{image-is-directed}\AgdaSpace{}\AgdaBound{ğ““}\AgdaSpace{}\AgdaBound{ğ“”}\AgdaSpace{}\AgdaBound{g}\AgdaSpace{}\AgdaBound{Î´} is a proof that the family $Î» i. f (g , Î± i)$ is directed. We label this application of continuity $i$. We then show a proof that \AgdaUpperBound{ğ“”}{\AgdaFunction{image-is-directed}\AgdaSpace{}\AgdaBound{ğ““}\AgdaSpace{}\AgdaBound{ğ“”}\AgdaSpace{}\AgdaBound{g}\AgdaSpace{}\AgdaBound{Î´}} is less than $uâ‚$, which follows by the definition of the least upper bound. We label this proof $ii$. Therefore, due to the equality we shown in $i$, we transport the property shown in $ii$, to show that \AgdaFunction{f}\AgdaSpace{}\AgdaPair{g}{\AgdaUpperBound{ğ““}{Î´}} is less than $uâ‚$.
\begin{code}
              l : (uâ‚ : âŸ¨ ğ“” âŸ©) â†’ ((i : I) â†’ f (g , Î± i) âŠ‘âŸ¨ ğ“” âŸ© uâ‚) â†’ f (g , âˆ ğ““ Î´) âŠ‘âŸ¨ ğ“” âŸ© uâ‚
              l uâ‚ p = transport (Î» - â†’ - âŠ‘âŸ¨ ğ“” âŸ© uâ‚) (i â»Â¹) ii
                where
                  i : f (g , âˆ ğ““ Î´) â‰¡ âˆ ğ“” (image-is-directed ğ““ ğ“” g Î´)
                  i = continuous-function-âˆ-â‰¡ ğ““ ğ“” g Î´
                  ii : (âˆ ğ“” (image-is-directed ğ““ ğ“” g Î´)) âŠ‘âŸ¨ ğ“” âŸ© uâ‚
                  ii = âˆ-is-lowerbound-of-upperbounds ğ“” (image-is-directed ğ““ ğ“” g Î´) uâ‚ p
\end{code}
}
We can then construct \AgdaFunction{evalâŠ¥} for convenience, similar to as we have previously, to work on elements from \AgdaFunction{DCPOâŠ¥} rather than \AgdaFunction{DCPO}.

With the above construction, we can now form our interpretation of application as the composition of our \AgdaFunction{to-Ã—-DCPOâŠ¥} construction which we use to evaluate two terms under a context simultaneously, and \AgdaFunction{evalâŠ¥} which performs the application as we would expect.
\end{AgdaAlign}

\subsubsection*{Interpretation function of terms}

We begin with our interpretation function definition.
\begin{code}
âŸ¦_âŸ§â‚‘ : {n : â„•} {Î“ : Context n} {Ïƒ : type} (t : PCF Î“ Ïƒ) â†’ DCPOâŠ¥[ ã€ Î“ ã€‘ , âŸ¦ Ïƒ âŸ§ ]
\end{code}
We first consider the interpretation for the PCF term \AgdaZero{} under the context \AgdaBound{Î“}. This term has no free variables, and we always want it to correspond to the natural number \AgdaInductiveConstructor{zero} in \AgdaDatatype{â„•}. So, we define a constant function where the output is always the total element \AgdaInductiveConstructor{zero} of the lifted set \AgdaFunction{ğ“›}\AgdaSpace{}\AgdaDatatype{â„•}. Since any constant function is continuous, then the function we have defined must be continuous.
\begin{code}
âŸ¦ Zero {_} {Î“} âŸ§â‚‘ = (Î» _ â†’ Î· zero) , const-functions-are-continuous âŸª ã€ Î“ ã€‘ âŸ« âŸª âŸ¦ Î¹ âŸ§ âŸ« (Î· zero)
\end{code}
We next define the interpretation of \AgdaSucc{t}, under a context \AgdaBound{Î“}. Here we make use of \AgdaFunction{ğ“›Ì‡}, which is responsible for taking a function between two types, and producing a function between the lifting of the types. It maps elements as the original function would, just with the extra mapping of $âŠ¥$ to $âŠ¥$. The lifted function is also shown to be continuous. We compose this function with the interpretation of the term \AgdaBound{t}. We can view this first determining the value of \AgdaBound{t} under a context \AgdaBound{Î“}, and then taking the successor of the result.
\begin{code}
âŸ¦ Succ {_} {Î“} t âŸ§â‚‘ = [ ã€ Î“ ã€‘ , âŸ¦ Î¹ âŸ§ , âŸ¦ Î¹ âŸ§ ]
                     (ğ“›Ì‡ succ , ğ“›Ì‡-continuous â„•-is-set â„•-is-set succ) âˆ˜áµˆá¶œáµ–áµ’âŠ¥ âŸ¦ t âŸ§â‚‘ 
\end{code}
The interpretation of \AgdaInductiveConstructor{Pred}\AgdaSpace{}\AgdaBound{t} under a context \AgdaBound{Î“} is then similar to that of \AgdaSucc{t}, except we use the function \AgdaFunction{pred} instead, which maps all natural numbers $n$ to $n-1$, apart from $0$ which maps to $0$.
\begin{code}
âŸ¦ Pred {_} {Î“} t âŸ§â‚‘ = [ ã€ Î“ ã€‘ , âŸ¦ Î¹ âŸ§ , âŸ¦ Î¹ âŸ§ ]
                     (ğ“›Ì‡ pred , ğ“›Ì‡-continuous â„•-is-set â„•-is-set pred) âˆ˜áµˆá¶œáµ–áµ’âŠ¥ âŸ¦ t âŸ§â‚‘
\end{code}
For \AgdaIfZero{t}{tâ‚}{tâ‚‚} under a context \AgdaBound{Î“}, we interpret this using the DCPO with $âŠ¥$ we constructed previously. The order of the arguments is intentionally switched, as we define our arguments to \AgdaInductiveConstructor{IfZero} in the same order as Streicher \cite[Section~2]{domaintheoreticfoundations}, however we use the interpretation from Tom de Jong, who defines his in a slightly different order \cite{jong2019scott}.
\begin{code}
âŸ¦ IfZero {_} {Î“} t tâ‚ tâ‚‚ âŸ§â‚‘ = â¦…ifZeroâ¦†Î“ Î“ âŸ¦ tâ‚ âŸ§â‚‘ âŸ¦ tâ‚‚ âŸ§â‚‘  âŸ¦ t âŸ§â‚‘
\end{code}
Now we consider the interpretation of \AgdaLambda{t}. Since \AgdaLambda{t} has a type of \AgdaBound{Ïƒ}\AgdaSpace{}\AgdaInductiveConstructor{â‡’}\AgdaSpace{}\AgdaBound{Ï„}, we need to produce a continuous function from \AgdaContextInterp{Î“} to the continuous function space \FuncDcpoBot{\AgdaTypeInterp{Ïƒ}}{\AgdaTypeInterp{Ï„}}. However, from the interpretation of \AgdaBound{t}, we have a continuous function from \AgdaContextInterp{Î“ â€™ Ïƒ} to \AgdaTypeInterp{Ï„}. We can therefore just apply the currying operation to \AgdaTermInterp{t}.
\begin{code}
âŸ¦ Æ› {_} {Î“} {Ïƒ} {Ï„} t âŸ§â‚‘ = curryáµˆá¶œáµ–áµ’âŠ¥ ã€ Î“ ã€‘ âŸ¦ Ïƒ âŸ§ âŸ¦ Ï„ âŸ§ âŸ¦ t âŸ§â‚‘ 
\end{code}
As we previously explored, we represent the application of a term \AgdaBound{t} to a term \AgdaBound{tâ‚} as first evaluating both terms under a list of values to substitute for their free variables, and then performing the evaluation as specified by \AgdaFunction{evalâŠ¥}.
\begin{code}
âŸ¦ _Â·_ {_} {Î“} {Ïƒ} {Ï„} t tâ‚ âŸ§â‚‘ = [ ã€ Î“ ã€‘ , ( âŸ¦ Ïƒ â‡’ Ï„ âŸ§ Ã—áµˆá¶œáµ–áµ’âŠ¥ âŸ¦ Ïƒ âŸ§) , âŸ¦ Ï„ âŸ§ ]
                 (evalâŠ¥ âŸ¦ Ïƒ âŸ§ âŸ¦ Ï„ âŸ§) âˆ˜áµˆá¶œáµ–áµ’âŠ¥ (to-Ã—-DCPOâŠ¥ ã€ Î“ ã€‘ âŸ¦ Ïƒ â‡’ Ï„ âŸ§ âŸ¦ Ïƒ âŸ§ âŸ¦ t âŸ§â‚‘ âŸ¦ tâ‚ âŸ§â‚‘) 
\end{code}
For a variable \AgdaVar{x} with type \AgdaBound{Ïƒ} under the context \AgdaBound{Î“}, we use our \AgdaFunction{var-extract} function we previously defined applied to \AgdaBound{x}. This provides us with a continuous function from the interpretation of the context \AgdaBound{Î“} to the interpretation of the type \AgdaBound{Ïƒ}.
\begin{code}
âŸ¦ v x âŸ§â‚‘ = var-extract x
\end{code}
When interpreting \AgdaFix{t}, we make use of Tom de Jong's interpretation of \AgdaInductiveConstructor{Fix} in his setting. The function \AgdaFunction{Î¼} provides us an interpretation of the fixed-point combinator for a type \AgdaBound{Ïƒ}. Therefore, this makes \AgdaFunction{Î¼}\AgdaSpace{}\AgdaTypeInterp{Ïƒ} a continuous function between the DCPOs \AgdaTypeInterp{\AgdaBound{Ïƒ}\AgdaSpace{}\AgdaInductiveConstructor{â‡’}\AgdaSpace{}\AgdaBound{Ïƒ}} and \AgdaTypeInterp{Ïƒ}. Since we want to apply this fixed-point combinator to the interpretation of \AgdaBound{t}, we compose \AgdaFunction{Î¼}\AgdaSpace{}\AgdaTypeInterp{Ïƒ} with the interpretation of \AgdaBound{t}. This allows us to form a new continuous function which takes a list of values to substitute for the free variables in \AgdaBound{t}, and applies the fixed-point combinator to return a result of the type \AgdaTypeInterp{Ïƒ}, which is the fixed point of \AgdaTermInterp{t} after substituting the list of values for free variables.
\begin{code}
âŸ¦ Fix {_} {Î“} {Ïƒ} t âŸ§â‚‘ = [ ã€ Î“ ã€‘ , âŸ¦ Ïƒ â‡’ Ïƒ âŸ§ , âŸ¦ Ïƒ âŸ§ ] (Î¼ âŸ¦ Ïƒ âŸ§) âˆ˜áµˆá¶œáµ–áµ’âŠ¥ âŸ¦ t âŸ§â‚‘
\end{code}


\end{document}
